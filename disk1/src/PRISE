10  REM  GREETING-1 PROGRAM
20  CLS : PRINT : PRINT 
30  DEF  FN ASN(X) =  ATN(X/ SQR( -X *X +1))
40  DEF  FN ACO(X) =  - ATN(X/ SQR( -X *X +1)) +1.5707963
50  DEF  FN RAD(X) = .01745328 *(X)
60  DEF  FN DEG(X) = 57.29578 *(X)
70  REM  INSERT LOCAL LONGITUDE AND LATITUDE IF FOLLOWING STATEMENT
80 LO = 122.49:LA = 38.24
90  PRINT : PRINT : PRINT : PRINT 
100  PRINT  TAB( 10)"ASTRONOMY PROGRAM": PRINT 
110  PRINT  TAB( 9)"-------------------"
120  PRINT  TAB( 9)"I  INNER PLANETS  I"
130  PRINT  TAB( 9)"I TIMES FROM SUN  I"
140  PRINT  TAB( 9)"-------------------"
150  PRINT 
160  PRINT  TAB( 7)"BY ERIC BURGESS F.R.A.S."
170  PRINT 
180  PRINT  TAB( 7)"ALL RIGHTS RESERVED BY"
190  PRINT  TAB( 7)"S & T SOFTWARE SERVICE"
200  FOR J = 2000 TO 1  STEP  -1: NEXT J
210  CLS : PRINT : PRINT : PRINT 
220  PRINT : PRINT : PRINT : PRINT 
230  PRINT "THIS PROGRAM GIVES APPROXIMATE"
240  PRINT "TIME IN MINUTES THAT MERCURY AND"
250  PRINT "VENUS RISE BEFORE THE SUN AS"
260  PRINT "MORNING STARS, OR SET AFTER THE"
270  PRINT "SUN AS EVENING STARS, FOR ANY DATE"
280  PRINT "       WHICH YOU INPUT"
290  PRINT : PRINT 
300  PRINT "INITIAL CONDITIONS ARE SET FOR......"
310  PRINT  TAB( 5)"SEBASTOPOL, CA LONG. 122.49"
320  PRINT  TAB( 21)"LAT. 38.24": PRINT 
330  PRINT : INPUT "WANT TO CHANGE THEM Y/N? ";A$
340  IF A$ < >"Y" GOTO 380
350  CLS : PRINT : PRINT : PRINT : PRINT 
360  INPUT "    GIVE LATITUDE ";LA
370  INPUT "    AND LONGITUDE ";LO
380  CLS : PRINT : PRINT : PRINT 
390  PRINT "ENTER THE DATE": PRINT 
400 FL = 2
410  INPUT "  THE YEAR ? ";YD$:Y =  VAL(YD$)
420  IF Y = 0  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 410
430  IF Y >1800  THEN  GOTO 490
440  PRINT "IS ";Y;" THE CORRECT YEAR? "
450  INPUT Y$
460  IF Y$ = "Y"  THEN  GOTO 490
470  IF Y$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 440
480  IF Y$ = "N"  THEN  PRINT : GOTO 410
490 LY = 0
500  IF Y/4 - INT(Y/4) = 0  AND Y/100 - INT(Y/100) < >0  THEN LY = 1
505  IF Y/400 - INT(Y/400) = 0  THEN LY = 1
510  PRINT : INPUT "  THE MONTH? ";MD$:M =  VAL(MD$)
520  IF M = 0  OR M >12  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 490
530  PRINT : INPUT "  THE DAY? ";DD$:D =  VAL(DD$)
540  IF D = 0  OR D >31  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 530
550  IF M < >2  THEN  GOTO 580
560  IF LY = 0  AND D >28  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 530
570  IF LY = 1  AND D >29  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 530
580 DX = D:MX = M:YX = Y
590 D = D +(LO/15)/24
600  IF (LY = 1  AND M = 2  AND  INT(D) >29)  THEN M = 3:D = D -29: GOTO 660
610  IF (LY = 0  AND M = 2  AND  INT(D) >28)  THEN M = 3:D = D -28: GOTO 660
620  IF D <31 GOTO 660
630  IF (M = 4  OR M = 6  OR M = 9  OR M = 11)  THEN M = M +1:D = D -30: GOTO 660
640  IF  INT(D) >31  THEN M = M +1:D = D -31
650  IF M = 13  THEN M = 1:Y = Y +1
660  REM  CALC DAYS TO DATE REQUESTED
670  REM  FROM 1960,1,1 EPOCH
680 DG = 365 *Y +D +(M -1) *31
690  IF M > = 3  THEN  GOTO 730
700  REM  CALC FOR JAN OR FEB
710 DG = DG + INT((Y -1)/4) - INT((.75) * INT((Y -1)/100 +1))
720  GOTO 750
730  REM  CALC FOR MAR THRU DEC
740 DG = DG - INT(M *.4 +2.3) + INT(Y/4) - INT((.75) * INT((Y/100) +1))
750 NI = DG -715875
760  GOSUB 1620: REM  GET SIDEREAL TIME
770  REM  INPUT OF ORBIT DATA FOR PLANETS
780  IF F = 1  THEN  GOTO 950
790  RESTORE 
800  DIM PD(3,9)
810  FOR YY = 0 TO 2: FOR XX = 0 TO 8
820  READ PD(YY,XX)
830  NEXT XX,YY
840  REM  MERCURY
850  DATA .071422,3.8484,.388301,1.34041,.3871,.07974,2.73514
860  DATA .12223,.836013
870  REM  VENUS
880  DATA .027962,3.02812,.013195,2.28638,.7233,.00506,3.85017
890  DATA .059341,1.33168
900  REM  EARTH
910  DATA .017202,1.74022,.032044,1.78547,1,.017,3.33926
920  DATA 0,0
930  FOR I9 = 1 TO 3: READ P$(I9): NEXT I9
940  DATA  MERCURY,VENUS,SUN
950 F = 1
960  REM  CALC DATA FOR PLANETS
970  CLS 
980  PRINT : PRINT : PRINT 
990  PRINT "DATA REQUESTED FOR ";YX;" ";MX;" ";DX
1000  PRINT : PRINT : PRINT 
1010  PRINT :I = 1
1020  FOR J = 0 TO 2: GOSUB 1230
1030 A(I) = A:D(I) = DS:L(I) = L
1040 I = I +1: NEXT 
1050  FOR I = 1 TO 2
1060  REM  SKIP EARTH
1070  IF I = 3  THEN  I = 4
1080  GOSUB 1370
1090 Q(I) = Q:X(I) = X:R(I) = R:V(I) = V
1100  NEXT I
1110  FOR K = 1 TO 2
1120 I = K:A(I) =  FN DEG(A(I))
1130  GOSUB 1710
1140  NEXT 
1150  PRINT : PRINT : PRINT 
1160  INPUT "WANT ANOTHER DATE Y/N? ";A$
1170  IF A$ = "Y"  THEN  GOTO 380
1180  IF A$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1160
1190  CLS 
1200  RUN "D:ASTRO"
1210  REM  CALC POSITION DATA
1220  REM  CALC HELIOCENTRIC LONGITUDE
1230 A = NI *PD(J,0) +PD(J,1)
1240  IF A >6.28318  THEN A = ((A/6.28318) - INT(A/6.28318)) *6.28318
1250  IF A <0  THEN A = A +6.28318: GOTO 1250
1260 C = PD(J,2) * SIN(A -PD(J,3))
1270 A = A +C
1280  IF A >6.28318  THEN A = A -6.28318
1290  IF A <0  THEN A = A +6.28318: GOTO 1290
1300  REM  CALC PLANET DIST FROM SUN
1310 DS = PD(J,4) +PD(J,5) * SIN(A -PD(J,6))
1320  REM  CALC DIST FROM ECLIPTIC
1330 L = PD(J,7) * SIN(A -PD(J,8))
1340  RETURN 
1350  REM  CALC PLANETARY DATA
1360  REM  CALC ANG DISTANCE FROM EARTH
1370 Z = A(3) -A(I)
1380  IF  ABS(Z) >3.14159  AND Z <0  THEN Z = Z +6.28318
1390  IF  ABS(Z) >3.14159  AND Z >0  THEN Z = Z -6.28318
1400  REM   CALC DISTANCE FROM EARTH
1410 Q =  SQR(D(I) ^2 +D(3) ^2 -2 *D(I) *D(3) * COS(Z))
1420  REM  CALC ANG DISTANCE FROM SUN
1430 P = (D(I) +D(3) +Q)/2
1440 X = 2 * FN ACO( SQR(((P *(P -D(I)))/(D(3) *Q))))
1450  REM  CALC RA OF PLANET
1460  IF Z <0  THEN R =  FN DEG(A(3) +3.14159 -X)/15
1470  IF Z >0  THEN R =  FN DEG(A(3) +3.14159 +X)/15
1480  IF R >24  THEN R = R -24: GOTO 1480
1490  IF R < -24  THEN R = R +24: GOTO 1490
1500  IF R <0  THEN R = R +24: GOTO 1500
1510  REM  CALC DECLINATION
1520  IF Z <0  THEN V =  SIN(A(3) +3.14159 -X) *23.44194 + FN DEG(L(I))
1530  IF Z >0  THEN V =  SIN(A(3) +3.14159 +X) *23.44194 + FN DEG(L(I))
1540 X =  FN DEG(X)
1550 R(3) =  FN DEG(A(3) +3.14159)/15
1560  IF R(3) >24  THEN R(3) = R(3) -24: GOTO 1560
1570  IF R(3) < -24  THEN R(3) = R(3) +24: GOTO 1570
1580  IF R(3) <0  THEN R(3) = R(3) +24
1590 V(3) =  SIN(A(3) +3.14159) *23.44194
1600  RETURN 
1610  RETURN 
1620  REM  CALC SIDEREAL TIME
1630 GC = 11.927485
1640 TC = .065711
1650 T2 = TC *(NI -7020) +GC
1660  IF T2 >24  THEN T2 = T2 -24: GOTO 1660
1670  IF T2 < -24  THEN T2 = T2 +24: GOTO 1670
1680  IF T2 <0  THEN T2 = T2 +24
1690  RETURN 
1700  REM  GET TIME DIFFERENCES
1710 A1 =  SIN( FN RAD(V(3))) * SIN( FN RAD(LA))
1720  IF A1 <0  THEN A1 =  ABS(A1): GOTO 1740
1730  IF A1 >0  THEN A1 =  -A1
1740 A1 = (A1 -.01454)/ COS( FN RAD(LA))
1750 A1 = A1/ COS( FN RAD(V(3)))
1760 B1 =  SIN( FN RAD(D(I))) * SIN( FN RAD(LA))
1770  IF B1 <0  THEN B1 =  ABS(B1): GOTO 1790
1780  IF B1 >0  THEN B1 =  -B1
1790 B1 = (B1 -.00989)/ COS( FN RAD(LA))
1800 B1 = B1/ COS( FN RAD(V(I)))
1810 C1 = 4 *(A1 -B1)
1820 D1 = 60 *(R(3) -R(I))
1830 M1 = C1 +D1
1840  REM   ADJUST FOR RA PASSING ZERO
1850  IF M1 >720  THEN M1 = M1 -1440: GOTO 1870
1860  IF M1 < -720  THEN M1 = M1 +1440
1870  IF M1 <0  THEN AS$ = "AN EVENING STAR ":MS$ = "SETTING":BA$ = " AFTER"
1880  IF M1 >0  THEN AS$ = " A MORNING STAR ":MS$ = "RISING ":BA$ = " BEFORE"
1890 M1 =  ABS(M1)
1900 M1$ =  LEFT$( STR$(M1),4)
1910  PRINT P$(I);" IS ";AS$
1920  PRINT " ";MS$;" ";M1$;" MINUTES ";BA$;" THE SUN"
1930  PRINT : PRINT : PRINT 
1940  RETURN 
