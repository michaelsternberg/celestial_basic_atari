10  REM  INNER PLANETS' ELONGATIONS
20  DEF  FN ACO(X) =  - ATN(X/ SQR( -X *X +1)) +1.5707963
30 P1 = 3.14159:P2 = 6.28318
40  CLS : PRINT : PRINT : PRINT 
50  PRINT  TAB( 10)"AN ASTRONOMY PROGRAM"
60  PRINT : PRINT 
70  PRINT  TAB( 10)"--------------------"
80  PRINT  TAB( 10)"I  INNER  PLANETS  I"
90  PRINT  TAB( 10)"--------------------"
100  PRINT : PRINT 
110  PRINT  TAB( 9)"BY ERIC BURGESS F.R.A.S."
120  PRINT : PRINT 
130  PRINT  TAB( 9)"ALL RIGHTS RESERVED BY"
140  PRINT  TAB( 9)"S & T SOFTWARE SERVICE"
150  PRINT : PRINT : PRINT 
160  INPUT "DO YOU WANT INSTRUCTIONS Y/N? ";A$
170  IF A$ = "N"  THEN  CLS : PRINT : PRINT : PRINT : GOTO 350
180  IF A$ < >"Y"  THEN  PRINT "INVALID RESPONSE:PRINT:GOTO140
190  CLS : PRINT : PRINT : PRINT 
200  PRINT "THIS PROGRAM OFFERS TWO ALTERNATIVES"
210  PRINT : PRINT 
220  PRINT "   1) CALCULATES APPROXIMATE ANGULAR"
230  PRINT "      DISTANCES OF MERCURY AND VENUS"
240  PRINT "      FROM THE SUN, AND APPROXIMATE"
250  PRINT "      DISTANCES FROM EARTH, AND"
260  PRINT "      PROVIDES APPROXIMATE ANGULAR"
270  PRINT "      DIAMETER AND PHASE FOR ANY DATE"
280  PRINT 
290  PRINT "   2) PROVIDES DATE OF THE NEXT"
300  PRINT "      ELONGATION OF MERCURY OR VENUS"
310  PRINT "          AFTER ANY DATE"
320  PRINT : PRINT 
330  INPUT "TO CONTINUE PRESS RETURN ";A$
340  CLS : PRINT : PRINT : PRINT 
350  PRINT "SELECT 1) ANGULAR DISTANCES"
360  PRINT "       2) NEXT ELONGATION"
370  PRINT : INPUT B$
380  IF B$ = "1"  THEN 410
390  IF B$ < >"2"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 340
400  GOTO 2110
410  GOSUB 890: REM  GET DISTANCE FROM EARTH
420  CLS : PRINT : PRINT 
430  GOSUB 440: GOTO 820
440 MD$ =  LEFT$( STR$(6.68/MQ),5)
450 VD$ =  LEFT$( STR$(16.82/VQ),5)
460  PRINT "FOR YEAR ";Y;" MONTH ";M;" DAY ";D: PRINT 
470  PRINT "---------------------------": PRINT 
480 MQ$ =  LEFT$( STR$(MQ),4)
490 VQ$ =  LEFT$( STR$(VQ),4)
500  IF E$ = "2"  THEN  GOTO 540
510  PRINT "DISTANCE OF MERCURY ";MQ$;" A.U."
520  PRINT  TAB( 10)"OR "; VAL( LEFT$( STR$(MQ *92.9),5));" MILLION MI"
530  IF E$ = "1"  THEN  GOTO 560
540  PRINT "DISTANCE OF VENUS ";VQ$;" A.U."
550  PRINT  TAB( 10)"OR "; VAL( LEFT$( STR$(VQ *92.9),5));" MILLION MI"
560  PRINT 
570 MW$ =  LEFT$( STR$(MW *57.29578),4)
580 VW$ =  LEFT$( STR$(VW *57.29578),4)
590  IF E$ = "2"  THEN  GOTO 620
600  PRINT "MERCURY'S ANG. DIST. FROM SUN ";MW$;" DEG."
610  IF E$ = "1"  THEN  PRINT : GOTO 640
620  PRINT "VENUS'S ANGULAR DIST. FROM SUN ";VW$;" DEG."
630  PRINT 
640  IF E$ = "2"  THEN  GOTO 690
650  PRINT "MERCURY'S ANG. DIAMETER ";MD$;" ARCSEC"
660  PRINT " MAX AT INFER. CONJ. IS 12 SEC"
670  IF E$ = "1"  THEN  GOTO 710
680  PRINT 
690  PRINT "VENUS'S ANG. DIAMETER ";VD$;" ARCSEC"
700  PRINT " MAX AT INFER. CONJ. IS 61 SEC"
710  PRINT 
720 PH =  ABS(MW) + ABS(MZ): GOSUB 2880
730  IF E$ = "2"  THEN  GOTO 760
740  PRINT "MERCURY'S PHASE ";PH$;" ";PH;" DEG ILLUM"
750  IF E$ = "1"  THEN  GOTO 820
760 PH =  ABS(VW) + ABS(ZV): GOSUB 2880
770  PRINT "PHASE OF VENUS   ";PH$;" ";PH;" DEG ILLUM"
780 :
790  IF E$ = "2"  THEN 820
800  GOSUB 1790
810  GOSUB 1970
820 :
830  IF B$ = "2"  THEN  RETURN 
840  INPUT "WANT ANOTHER DATE Y/N ? ";A$
850  IF A$ = "N"  THEN  CLS : GOTO 880
860  IF A$ < >"Y"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 840
870  CLS : PRINT : PRINT : PRINT : GOTO 410
880  RUN "D:ASTRO"
890  REM  ROUTINE FOR ANG. DIAMETER
900  CLS : PRINT : PRINT : PRINT 
910  GOSUB 1180: REM  ENTER DATE
920  GOSUB 1360
930  REM  CALC DIST. FROM EARTH
940  REM  AND ANG. DISTANCE FROM SUN
950 AE = NI *.017202 +1.74022
960  IF AE >P2  THEN AE = ((AE/P2) - INT(AE/P2)) *P2
970  IF AE <0  THEN AE = AE +P2: GOTO 970
980 CE = .032044 * SIN(AE -1.78547)
990 AE = AE +CE
1000  IF AE >P2  THEN AE = AE -P2
1010  IF AE <0  THEN AE = AE +P2: GOTO 1010
1020 DE = 1 +.017 * SIN(AE -3.33926)
1030  GOSUB 1470
1040 MZ = AE -MA
1050 ZM = MZ
1060  IF  ABS(MZ) >P1  AND MZ <0  THEN MZ = MZ +P2
1070  IF  ABS(MZ) >P1  AND MZ >0  THEN MZ = MZ -P2
1080  REM  CALC DISTANCE FROM EARTH IN AU
1090 MQ =  SQR(MD ^2 +DE ^2 -2 *MD *DE * COS(MZ))
1100 MP = (MD +DE +MQ)/2
1110  REM  CALC ANG. DISTANCE FROM SUN
1120 MV =  SQR((MP *(MP -MD))/(DE *MQ))
1130 MW = 2 * FN ACO(MV)
1140  IF  ABS(ZM) >P1  THEN ZM = ZM +P2
1150  IF ZM >0  THEN MW =  -MW
1160  GOSUB 1570
1170  RETURN 
1180  REM  ENTER DATE
1190  PRINT : PRINT 
1200  PRINT : PRINT "ENTER THE DATE ": PRINT 
1210  INPUT "THE YEAR ";Y$:Y =  VAL(Y$)
1220  IF Y = 0  THEN  PRINT "INVALID RESPONSE"; PRINT : GOTO 1210
1230  IF Y >1800 GOTO 1280
1240  PRINT "IS ";Y;" THE CORRECT YEAR": INPUT A$
1250  IF A$ = "Y"  THEN 1280
1260  IF A$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1240
1270  IF A$ = "N"  THEN 1210
1280  PRINT : INPUT "THE MONTH ";M$:M =  VAL(M$)
1290  IF M = 0  OR M >12  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1280
1300  PRINT : INPUT "THE DAY ";D$:D =  VAL(D$)
1310  IF D = 0  OR D >31  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1300
1320  IF M = 2  AND M >29  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1300
1330  REM  STORE INITIAL DATE AND TIME
1340 YI = Y:MI = M:DI = D
1350  RETURN 
1360  REM  CALC DAYS FROM EPOCH 1960
1370 DG = 365 *Y +D +((M -1) *31)
1380  IF M > = 3 GOTO 1420
1390  REM  CALC IF JAN OR FEB
1400 DG = DG + INT((Y -1)/4) - INT((.75) * INT((Y -1)/100 +1))
1410  GOTO 1440
1420  REM  CALC FOR MAR THRU DEC
1430 DG = DG - INT(M *.4 +2.3) + INT(Y/4) - INT((.75) * INT((Y/100) +1))
1440 NI = DG -715875
1450  RETURN 
1460  GOTO 410
1470  REM  CALC MERCURY'S POSITION
1480 MA = NI *.071425 +3.8494
1490  IF MA >P2  THEN MA = ((MA/P2) - INT(MA/P2)) *P2
1500  IF MA <0  THEN MA = MA +P2: GOTO 1500
1510 MC = .388301 * SIN(MA -1.34041)
1520 MA = MA +MC
1530  IF MA >P2  THEN MA = MA -P2
1540  IF MA <0  THEN MA = MA +P2: GOTO 1540
1550 MD = .3871 +.079744 * SIN(MA -2.73514)
1560  RETURN 
1570  REM  CALC VENUS'S POSITION
1580 VA = NI *.027962 +3.02812
1590  IF VA >P2  THEN VA = ((VA/P2) - INT(VA/P2)) *P2
1600  IF VA <0  THEN VA = VA +P2: GOTO 1600
1610 VC = .013195 * SIN(VA -2.28638)
1620 VA = VA +VC
1630  IF VA >P2  THEN VA = VA -P2
1640  IF VA <0  THEN VA = VA +P2: GOTO 1640
1650 VD = .7233 +.00506 * SIN(VA -3.85017)
1660 ZV = AE -VA
1670 VZ = ZV
1680  IF  ABS(ZV) >P1  AND ZV <0  THEN ZV = ZV +P2
1690  IF  ABS(ZV) >P1  AND ZV >0  THEN ZV = ZV -P2
1700  REM  CALC DISTANCE FROM EARTH IN AU
1710 VQ =  SQR(VD ^2 +DE ^2 -2 *VD *DE * COS(ZV))
1720 VP = (VD +DE +VQ)/2
1730  REM  CALC ANG. DISTANCE FROM SUN
1740 VV =  SQR((VP *(VP -VD))/(DE *VQ))
1750 VW = 2 * FN ACO(VV)
1760  IF  ABS(VZ) >P1  THEN VZ = VZ +P2
1770  IF VZ >0  THEN VW =  -VW
1780  RETURN 
1790  REM  SHOW SUN AND PLANETS
1800 Z$(0) = MW$ +"M":Z$(1) = VW$ +"V"
1810 Z$(2) =  STR$(0) +""
1820  FOR I = 0 TO 2
1830 E$(I) = "XXX"
1840  NEXT I
1850 K = 0
1860  FOR I = 0 TO 2
1870  IF Z$(I) = "XXX" GOTO 1950
1880  FOR J = I TO 2
1890  IF Z$(J) = "XXX" GOTO 1910
1900  IF  VAL(Z$(J)) < VAL(Z$(I))  THEN I = J: GOTO 1910
1910  NEXT J
1920 E$(K) = Z$(I):Z$(I) = "XXX"
1930 K = K +1
1940 I =  -1
1950  NEXT I
1960  RETURN 
1970  INPUT "FOR VISUAL CONFIGURATION PRESS RETURN";A$
1980  PRINT 
1990  PRINT "VISUAL CONFIGURATION ON ";YI;" ";MI;" ";DI
2000  PRINT 
2010  PRINT "V = VENUS; M = MERCURY; 0 = SUN"
2020  PRINT 
2030 M1 = 20 +.8 * VAL( LEFT$(E$(0),5))/2
2040  PRINT  TAB( M1); RIGHT$(E$(0),1);
2050 M2 = 20 +.8 * VAL( LEFT$(E$(1),5))/2
2060  PRINT  TAB( M2); RIGHT$(E$(1),1);
2070 M3 = 20 +.8 * VAL( LEFT$(E$(2),5))/2
2080  PRINT  TAB( M3); RIGHT$(E$(2),1)
2090  PRINT : PRINT 
2100  RETURN 
2110  CLS : REM  ROUTINE FOR ELONGATIONS
2120  PRINT : PRINT : PRINT 
2130  PRINT "SELECT MERCURY  (1)"
2140  INPUT "      OR VENUS  (2) ";E$
2150  IF E$ = "1"  THEN  GOSUB 2170: GOTO 2430
2160  GOSUB 2620
2170  CLS : GOSUB 1200: REM  GET DATE
2180 FL = 0
2190  GOSUB 1360: REM  CALC DAYS FROM EPOCH
2200  GOSUB 930: REM  CALC FOR EARTH AND PLANETS
2210 VW$ =  STR$(VW *57.29578)
2220  CLS 
2230  PRINT 
2240  PRINT YI;" ";MI;" ";DI
2250  PRINT : PRINT : PRINT : PRINT : PRINT 
2260 MW$ =  STR$(MW *57.29578): GOSUB 1790: GOSUB 2030
2270  PRINT : PRINT : PRINT : PRINT 
2280  PRINT "M = MERCURY, V = VENUS, O = SUN"
2290 DS = MW *57.29578
2300  IF FL = 0  AND  ABS(DS) <15  THEN D = D +7: GOTO 2350
2310 CK =  ABS(DS) - ABS(D2)
2320  IF CK <.06  AND CK > -.06  THEN 2400
2330  IF  ABS(DS) <18  THEN D = D +4: GOTO 2350
2340  IF CK <.1  OR CK > -.1  THEN D = D +1: GOTO 2350
2350  IF D >30  THEN M = M +1:D = D -30
2360  IF M >12  THEN M = 1:Y = Y +1
2370 D2 = DS:FL = 1
2380  GOSUB 1340
2390  GOTO 2190
2400  CLS : PRINT : PRINT : PRINT 
2410  PRINT "NEXT ELONGATION OF MERCURY ";Y;" ";M;" ";D
2420  PRINT : PRINT 
2430  IF MW <0  THEN P$ = "EAST OF SUN (AN EVENING STAR)": GOTO 2450
2440 P$ = "WEST OF SUN (A MORNING STAR)"
2450  PRINT "AND THE PLANET IS...."
2460  PRINT 
2470  PRINT  TAB( 5);P$
2480  GOSUB 1980
2490  INPUT "DO YOU WANT MORE DETAILS Y/N? ";A$
2500  IF A$ = "N"  THEN  CLS : PRINT : GOTO 2530
2510  IF A$ = "Y"  THEN  CLS : PRINT : GOSUB 440
2520  PRINT : GOSUB 1980: PRINT 
2530  INPUT "DO YOU WANT ANOTHER DATE Y/N? ";A$
2540  IF E$ = "2"  AND A$ = "Y"  THEN  GOTO 2620
2550  IF E$ = "1"  AND A$ = "Y"  THEN  GOTO 2170
2560  IF A$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 2530
2570  PRINT : PRINT 
2580  INPUT "DO YOU WANT ANOTHER PLANET Y/N? ";A$
2590  IF A$ = "Y"  THEN  GOTO 2110
2600  IF A$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 2580
2610  GOTO 880
2620  CLS : GOSUB 1200: REM  GET DATE
2630 FL = 0
2640  GOSUB 1360: REM  CALC DAYS FROM EPOCH
2650  GOSUB 930: REM  CALC FOR EARTH AND PLANETS
2660  CLS 
2670 MW$ =  STR$(MW *57.29578)
2680  PRINT 
2690  PRINT YI;" ";MI;" ";DI
2700  PRINT : PRINT : PRINT : PRINT : PRINT 
2710 VW$ =  STR$(VW *57.29578): GOSUB 1790: GOSUB 2030
2720  PRINT : PRINT : PRINT : PRINT 
2730  PRINT "V= VENUS, M = MERCURY, S = SUN"
2740 VS = VW *57.29578
2750  IF FL = 0  AND  ABS(VS) <40  THEN D = D +20: GOTO 2800
2760 CK =  ABS(VS) - ABS(V2)
2770  IF CK <.05  AND CK > -.05  THEN 2850
2780  IF  ABS(VS) <45  THEN D = D +10: GOTO 2800
2790  IF CK <.1  OR CK > -.1  THEN D = D +1: GOTO 2800
2800  IF D >30  THEN M = M +1:D = D -30
2810  IF M >12  THEN M = 1:Y = Y +1
2820 V2 = VS:FL = 1
2830  GOSUB 1340
2840  GOTO 2640
2850  CLS : PRINT : PRINT : PRINT 
2860  PRINT "NEXT ELONGATION OF VENUS IS ";Y;" ";M;" ";D
2870 MW = VW: PRINT : GOTO 2430
2880  REM  SUB FOR PHASES
2890 PH = 180 -PH *57.2958
2900  IF PH >150  THEN PH$ = "THIN CRESCENT"
2910  IF PH >120  AND PH <151  THEN PH$ = "FAT CRESCENT"
2920  IF PH >70  AND PH <121  THEN PH$ = "NEAR HALF"
2930  IF PH >29  AND PH <71  THEN PH$ = "GIBBOUS"
2940  IF PH <21  THEN PH$ = "NEAR FULL"
2950 PH =  INT(180 -PH)
2960  RETURN 
