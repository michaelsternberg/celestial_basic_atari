10  CLEAR 
20  REM  SATELLITES OF JUPITER
30  CLS : PRINT : PRINT : PRINT : PRINT 
40  DIM Z$(4),B$(4),Y$(4),M$(2),DA$(2),LT$(5)
50  DIM TZ$(2),ND$(1),IN$(2)
60  PRINT  TAB( 8)"AN ASTRONOMY PROGRAM"
70  PRINT 
80  PRINT  TAB( 6)"-------------------------"
90  PRINT  TAB( 6)"I  GALILEAN SATELLITES  I"
100  PRINT  TAB( 6)"-------------------------"
110  PRINT : PRINT 
120  PRINT  TAB( 7)"BY ERIC BURGESS F.R.A.S."
130  PRINT 
140  PRINT  TAB( 12)"VERSION APR.82"
150  PRINT 
160  PRINT  TAB( 8)"ALL RIGHTS RESERVED BY"
170  PRINT  TAB( 8)"S & T SOFTWARE SERVICE"
180  PRINT : PRINT 
190  FOR K = 3000 TO 1  STEP  -1: NEXT 
200  CLS : PRINT : PRINT : PRINT : PRINT 
210  PRINT  TAB( 5)"THIS PROGRAM SHOWS THE GALILEAN"
220  PRINT  TAB( 5)"SATELLITES POSITIONED RELATIVE"
230  PRINT  TAB( 5)"TO JUPITER AT THEIR APPROXIMATE"
240  PRINT  TAB( 11)"RELATIVE POSITIONS"
250  PRINT : PRINT 
260  PRINT "YOU CAN CHOOSE TO HAVE THEM SHOWN WITH"
270  PRINT "NORTH AT THE TOP, OR WITH SOUTH AT THE"
280  PRINT "TOP AS SEEN IN A TELESCOPE, AND YOU"
290  PRINT "CAN SELECT UP TO FIVE DISPLAYS OF THEIR"
300  PRINT "CONFIGURATIONS AT UP TO 24 HRS APART"
310  PRINT : PRINT : PRINT 
320  INPUT "DO YOU WANT SOUTH AT TOP (Y/N)? ";A$
330  IF A$ = "Y"  THEN FL = 1: GOTO 360
340  IF A$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 320
350 FL = 0
360  DEF  FN RAD(X) = (X) *3.14159/180
370  CLS : PRINT : PRINT : PRINT 
380  REM  ENTER DATE
390 LY = 0: PRINT "ENTER THE DATE"
400  PRINT : PRINT 
410  INPUT "YEAR ";Y$:Y =  VAL(Y$)
420  IF Y = 0  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 410
430  IF Y/4 - INT(Y/4) = 0  AND Y/100 - INT(Y/100) < >0  THEN LY = 1
435  IF Y/400 - INT(Y/400) = 0  THEN LY = 1
440  PRINT 
450  INPUT "MONTH ";M$
460 M =  VAL(M$)
470  IF M = 0  OR M >12  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 450
480  PRINT 
490  INPUT "DAY ";D$:DA =  VAL(D$)
500  IF DA = 0  OR DA >31  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 490
510  IF LY = 1  AND M = 2  AND DA <30  THEN  GOTO 550
520  IF LY = 0  AND M = 2  AND DA <29  THEN  GOTO 550
530  IF M < >2  THEN  GOTO 550
540  PRINT "INVALID RESPONSE": PRINT : GOTO 490
550  PRINT : INPUT "TIME ZONE ";TZ$:TZ =  VAL(TZ$)
560  IF TZ < -12  OR TZ >12  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 550
570  PRINT : INPUT "LOCAL TIME (DEC.HRS.) ";LT$
580 LT =  VAL(LT$)
590  IF LT <0  OR LT >24  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 570
600  PRINT 
610  INPUT "HOW MANY DISPLAYS (1 TO 5)? ";ND$:ND =  VAL(ND$)
620  IF ND <1  OR ND >5  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 610
630  PRINT 
640  INPUT "WHAT IS INTERVAL (UP TO 24 HR)? ";IN
650  IF IN <0  OR IN >24  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 640
660  CLS : PRINT : PRINT : PRINT 
670  PRINT "PROGRAM RUNNING...PLEASE WAIT"
680 L = 1
690 YP = Y:MP = M:DP = DA
700  IF L >1 GOTO 720
710 UT = LT +TZ
720  REM  CORRECT FOR DAY MONTH AND YR ENDS
730  IF UT >24  THEN UT = UT -24:DA = DA +1
740  IF (LY = 1  AND M = 2  AND DA = 30)  THEN M = 3:DA = 1: GOTO 790
750  IF (LY = 0  AND M = 2  AND DA = 29)  THEN M = 3:DA = 1: GOTO 790
760  IF DA <31 GOTO 790
770  IF (M = 4  OR M = 6  OR M = 9  OR M = 11)  THEN M = M +1:DA = 1: GOTO 790
780  IF DA >31  THEN M = M +1:DA = 1
790  IF M = 13  THEN M = 1:Y = Y +1
800  IF FL = 1  THEN 820
810 YP = Y:MP = M:DP = DA
820 T = UT/24
830 YC = Y:MC = M:DC = DA +T
840  IF MC >2  THEN 870
850 YC = Y -1
860 MC = M +12
870 TD =  INT(365.25 *YC) + INT((MC +1) *30.6001) +DC
890 YZ =  INT(YC/100)
900  REM  CALC DAYS SINCE 1900 EPOCH
910 I = TD -YZ +2 + INT(YZ/4) -694025.5
920  REM  DEG/DAY*NUMBR DAYS + POSTN AT EPOCH
930 A = I *203.40586 +84.55061
940 ZZ = A: GOSUB 2290
950 A = ZZ
960 B = I *101.2916323 +41.50155
970 ZZ = B: GOSUB 2290
980 B = ZZ
990 C = I *50.23451687 +109.97702
1000 ZZ = C: GOSUB 2290
1010 C = ZZ
1020 D = I *21.48798021 +176.35864
1030 ZZ = D: GOSUB 2290
1040 D = ZZ
1050  REM  ORBITAL MOTION IN PERIOD
1060  REM  EARTH AND JUPITER
1070 P1 = I *.9856003 +358.476
1080 ZZ = P1: GOSUB 2290
1090 P1 = ZZ
1100 I2 = I *.0830853 +225.328
1110 ZZ = I2: GOSUB 2290:I2 = ZZ
1120 P2 = I *.9025179 +221.647
1130 ZZ = P2: GOSUB 2290
1140 P2 = ZZ
1150 P2 = P2 +( SIN( FN RAD(2 *P1))/50) +( SIN( FN RAD(P1))/.521)
1160 P3 = ( SIN( FN RAD(2 *I2)) *.1673) +( SIN( FN RAD(I2)) *5.5372)
1170 P2 = P2 -P3
1180 P1 =  SQR(28.07 -( COS( FN RAD(P2)) *10.406))
1190 J = ( SIN( FN RAD(P2))/P1)
1200 I3 = 57.2958 *( ATN(( SIN( FN RAD(J)))/ SQR(1 -( SIN( FN RAD(J))) ^2)))
1210 I3 = I3 *57.2958
1220 P1 = P1/173
1230  REM  CALC RADIAL DISTANCE FROM JUPITER
1240 S1 = 5.906 * SIN( FN RAD(A +I3 -P3 -(P1 *203.405863)))
1250 S2 = 9.397 * SIN( FN RAD(B +I3 -P3 -(P1 *101.2916323)))
1260 S3 = 14.989 * SIN( FN RAD(C +I3 -P3 -(P1 *50.23451687)))
1270 S4 = 26.364 * SIN( FN RAD(D +I3 -P3 -(P1 *21.48798021)))
1280  IF FL < >1  THEN  GOTO 1370
1290  IF S1 <0  THEN S1 = 0 + ABS(S1): GOTO 1310
1300  IF S1 >0  THEN S1 = 0 - ABS(S1)
1310  IF S2 <0  THEN S2 = 0 + ABS(S2): GOTO 1330
1320  IF S2 >0  THEN S2 = 0 - ABS(S2)
1330  IF S3 <0  THEN S3 = 0 + ABS(S3): GOTO 1350
1340  IF S3 >0  THEN S3 = 0 - ABS(S3)
1350  IF S4 <0  THEN S4 = 0 + ABS(S4): GOTO 1370
1360  IF S4 >0  THEN S4 = 0 - ABS(S4)
1370  IF L >1  THEN  GOTO 1580
1380  CLS : PRINT : PRINT : PRINT 
1390  PRINT "DISTANCE IN TERMS OF JUPITER RADII"
1400  PRINT "WHICH IS 71,100 KM OR 44,180 MI"
1410  PRINT 
1420  PRINT "FOR DATE ";YP;" ";MP;" ";DP;" AT ";LT;" HRS TIME ZONE ";TZ
1430  PRINT "FOR DATE ";Y;" ";M;" ";DA;" AT ";UT;" HRS UT"
1440 EM = M
1450  PRINT : PRINT 
1460  PRINT  TAB( 5)"IO (I)......  ";S1
1470  PRINT  TAB( 5)"EUROPA (E)..  ";S2
1480  PRINT  TAB( 5)"GANYMEDE (G)  ";S3
1490  PRINT  TAB( 5)"CALLISTO (C)  ";S4
1500  PRINT : PRINT 
1510  PRINT "MINUS IS TO LEFT OF JUPITER"
1520  PRINT "   PLUS IS TO THE RIGHT": PRINT 
1530  IF FL = 1  THEN  PRINT  TAB( 4)"WITH S AT TOP ": GOTO 1550
1540  PRINT  TAB( 4)"WITH N AT TOP"
1550  PRINT : PRINT 
1560  INPUT "FOR GRAPHIC DISPLAY PRESS RETURN";A$
1570  CLS 
1580  GOSUB 2100
1590  REM  REGENERATE LMT
1600 LT = UT -TZ
1610  IF LT >0  THEN  GOTO 1690
1620 DP = DA -1:LT = LT +24: IF DP >0  THEN 1690
1630  IF DP <1  THEN MP = EM -1
1640  IF (LY = 1  AND MP = 2)  THEN DP = 29: GOTO 1690
1650  IF (LY = 0  AND MP = 2)  THEN DP = 28: GOTO 1690
1660  IF (MP = 4  OR MP = 6  OR MP = 9  OR MP = 11)  THEN DP = 30: GOTO 1690
1670 DP = 31
1680  PRINT 
1690  IF MP = 0  THEN MP = 12:YP = Y -1
1700  IF L >1  THEN  GOTO 1760
1710  IF FL = 1  THEN  PRINT  TAB( 20);"S": GOTO 1730
1720  PRINT  TAB( 20);"N"
1730  PRINT "JUPITER..J"; TAB( 20)"^"; TAB( 30);"IO ..... I"
1740  PRINT "EUROPA...E"; TAB( 20)"!"; TAB( 30);"GANYMEDE G"
1750  PRINT "CALLISTO C"; TAB( 20)"!"
1760  PRINT "--------------------------------------"
1770  PRINT YP;" ";MP;" ";DP;" AT ";LT;" HR  ZONE ";TZ
1780  PRINT " . . . . . . UT ";UT;" HRS ";EM;" ";DA
1790  PRINT 
1800 M1 = 20 + INT(.7 * VAL(B$(0)))
1810  PRINT  TAB( M1); RIGHT$(B$(0),1);
1820 M2 = 20 + INT(.7 * VAL(B$(1)))
1830  PRINT  TAB( M2); RIGHT$(B$(1),1);
1840 M3 = 20 + INT(.7 * VAL(B$(2)))
1850  PRINT  TAB( M3); RIGHT$(B$(2),1);
1860 M4 = 20 + INT(.7 * VAL(B$(3)))
1870  PRINT  TAB( M4); RIGHT$(B$(3),1);
1880 M5 = 20 + INT(.7 * VAL(B$(4)))
1890  PRINT  TAB( M5); RIGHT$(B$(4),1)
1900  IF L = ND  THEN  GOTO 2020
1910  REM  INCREMENT FOR INTERVAL
1920  REM  AND ADJUST MONTH ENDS
1930 UT = UT +IN
1940  IF UT >24  THEN UT = UT -24:DA = DA +1
1950  IF (LY = 1  AND M = 2  AND DA = 30)  THEN M = 3:DA = 1: GOTO 2000
1960  IF (LY = 0  AND M = 2  AND DA = 29)  THEN M = 3:DA = 1: GOTO 2000
1970  IF DA <31 GOTO 2000
1980  IF (M = 4  OR M = 6  OR M = 9  OR M = 11)  THEN M = M +1:DA = 1: GOTO 2000
1990  IF DA >31  THEN M = M +1:DA = 1
2000  IF M = 13  THEN M = 1:Y = Y +1
2010 L = L +1: GOTO 690
2020  PRINT : PRINT 
2030  INPUT "DO YOU WANT ANOTHER DATE Y/N? ";A$
2040  IF A$ = "N"  THEN  GOTO 2080
2050  IF A$ < >"Y"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 2030
2060  CLS 
2070 FL = 0: GOTO 320
2080  CLS 
2090  RUN "D:ASTRO"
2100  REM  SUB TO DISPLAY SATELLITES
2110 Z$(0) =  STR$(S1) +"I":Z$(1) =  STR$(S2) +"E"
2120 Z$(2) =  STR$(S3) +"G":Z$(3) =  STR$(S4) +"C"
2130 Z$(4) =  STR$(0) +"J"
2140  FOR I = 0 TO 4
2150 B$(I) = "****"
2160  NEXT I
2170 K = 0
2180  FOR I = 0 TO 4
2190  IF Z$(I) = "****" GOTO 2270
2200  FOR J = I TO 4
2210  IF Z$(J) = "****" GOTO 2230
2220  IF  VAL(Z$(J)) < VAL(Z$(I))  THEN I = J: GOTO 2230
2230  NEXT J
2240 B$(K) = Z$(I):Z$(I) = "****"
2250 K = K +1
2260 I =  -1
2270  NEXT I
2280  RETURN 
2290  REM  SUB FOR REDUCING ANGLES
2300 ZZ = (ZZ/360 - INT(ZZ/360)) *360
2310  IF ZZ <0  THEN ZZ = ZZ +360
2320  RETURN 
