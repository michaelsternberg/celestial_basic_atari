10  CLS : PRINT : PRINT 
20  PRINT : PRINT : PRINT 
30  DIM PD(9,9): DIM IP(10): DIM A(9): DIM DS(9): DIM L(9)
40  DIM Q(9): DIM X(9): DIM R(9): DIM V(9)
50  DEF  FN ASN(X) =  ATN(X/ SQR( -X *X +1))
60  DEF  FN ACO(X) =  - ATN(X/ SQR( -X *X +1)) +1.5707963
70  DEF  FN RAD(X) = .01745328 *(X)
80  DEF  FN DEG(X) = 57.29578 *(X)
90  REM  INSERT YOUR LOCAL LONGITUDE AND LATITUDE IN NEXT LINE
100 LO = 122.49:LA = 38.24
105 TZ = 8
110  PRINT  TAB( 8)"ASTRONOMY PROGRAM"
120  PRINT  TAB( 5)"------------------------"
130  PRINT  TAB( 5)"I  RISE AND SET TIMES  I"
140  PRINT  TAB( 5)"------------------------"
150  PRINT 
160  PRINT  TAB( 5)"BY ERIC BURGESS F.R.A.S."
170  PRINT : PRINT 
180  PRINT  TAB( 5)"ALL RIGHTS RESERVED BY"
190  PRINT  TAB( 5)"S & T SOFTWARE SERVICE"
200  PRINT : PRINT 
210  PRINT  TAB( 7)"VERSION OCT. 1983"
220  FOR J = 3000 TO 1  STEP  -1: NEXT J
230  CLS : PRINT : PRINT : PRINT 
240  PRINT : PRINT : PRINT 
250  PRINT "THIS PROGRAM GIVES APPROXIMATE TIMES"
260  PRINT "OF THE RISING, TRANSIT, AND SETTING"
270  PRINT "OF A SELECTED PLANET, OR THE SUN"
280  PRINT  TAB( 13)"OR THE MOON"
290  PRINT : PRINT : PRINT 
300  PRINT "INITIAL CONDITIONS ARE SET FOR"
310  PRINT 
320  REM  CHANGE NEXT TWO LINES TO YOUR LOCAL LAT AND LONG.
330  PRINT  TAB( 5)"SEBASTOPOL, CALIFORNIA"
340  PRINT  TAB( 5)"LONGITUDE 122.49; LATITUDE 38.24"
345  PRINT  TAB( 5)"TIME ZONE 8"
350  PRINT 
360  INPUT "DO YOU WANT TO CHANGE THEM Y/N? ";A$
370  IF A$ < >"Y" GOTO 430
380  CLS : PRINT : PRINT : PRINT 
390  PRINT : PRINT 
400  INPUT "GIVE LATITUDE ";LA
410  PRINT 
420  INPUT "AND LONGITUDE ";LO
425  PRINT : INPUT "AND TIME ZONE";TZ
430  CLS : PRINT : PRINT : PRINT : PRINT 
440  PRINT "ENTER THE DATE": PRINT 
445 TZ = LO/15 -TZ
450 FL = 2
460  INPUT "THE YEAR ";YD$:Y =  VAL(YD$)
470  IF Y = 0  THEN  PRINT "INVALID RESPONSE:PRINT:GOTO410
480  IF Y >1800  THEN  GOTO 540
490  PRINT "IS ";Y;" THE CORRECT YEAR? "
500  INPUT "Y/N ";Y$
510  IF Y$ = "Y"  THEN  GOTO 540
520  IF Y$ = "N"  THEN  PRINT : GOTO 460
530  PRINT "INVALID RESPONSE": PRINT : GOTO 490
540 LY = 0
550  IF Y/4 - INT(Y/4) = 0  AND Y/100 - INT(Y/100) < >0  THEN LY = 1
560  PRINT 
570  INPUT "THE MONTH ";MD$:M =  VAL(MD$)
580  IF M = 0  OR M >12  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 570
590  PRINT 
600  INPUT "THE DAY ";DD$:D =  VAL(DD$)
610  IF D = 0  OR D >31  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 600
620  IF M < >2  THEN  GOTO 650
630  IF LY = 0  AND D >28  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 600
640  IF LY = 1  AND D >29  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 600
650  GOSUB 2190: REM  TO SELECT PLANET
660 DX = D:MX = M:YX = Y
670 D = D +(LO/15)/24
680  IF (LY = 1  AND M = 2  AND  INT(D) >29)  THEN M = 3:D = D -29: GOTO 740
690  IF (LY = 0  AND M = 2  AND D >28)  THEN M = 3:D = D -28: GOTO 740
700  IF D <31  THEN  GOTO 740
710  IF (M = 4  OR M = 6  OR M = 9  OR M = 11)  THEN M = M +1:D = D -30: GOTO 740
720  IF  INT(D) >31  THEN M = M +1:D = D -31
730  IF M = 13  THEN M = 1:Y = Y +1
740  REM  CALC DAYS TO DATE REQUESTED FROM EPOCH 1960,1,1
750 DG = 365 *Y +D +((M -1) *31)
760  IF M >2  THEN  GOTO 800
770  REM  FOR JAN AND FEB
780 DG = DG + INT((Y -1)/4) - INT((.75) * INT((Y -1)/100 +1))
790  GOTO 820
800  REM  FOR MAR THRU DEC
810 DG = DG - INT(M *.4 +2.3) + INT(Y/4) - INT((.75) * INT((Y/100) +1))
820 NI = DG -715875
825 NI = NI +L0/180
830  GOSUB 2400: REM  TO GET SIDEREAL TIME (T2)
840  IF CI = 10  THEN  GOTO 2690
850  REM  INPUT OF PLANETARY ORBIT DATA
860  IF F = 1  THEN  GOTO 1210
870  RESTORE 
880  FOR PY = 0 TO 8: FOR PX = 0 TO 8
890  READ PD(PY,PX)
900  NEXT PX,PY
910  REM  MERCURY
920  DATA .071422,3.8484,.388301,1.34041
930  DATA .3871,.07974,2.73514,.12223,.836013
940  REM  VENUS
950  DATA .027962,3.02812,.013195,2.28638
960  DATA .7233,.00506,3.85017,.059341,1.33168
970  REM  EARTH
980  DATA .017202,1.74022,.032044,1.78547,1
990  DATA .017,3.33926,0,0
1000  REM  MARS
1010  DATA .009146,4.51234,.175301,5.85209,1.5237
1020  DATA  .141704,1.04656,.03142,.858702
1030  REM  JUPITER
1040  DATA .00145,4.53364,.090478,.23911,5.2028
1050  DATA  .249374,1.76188,.01972,1.74533
1060  REM  SATURN
1070  DATA .000584,4.89884,.105558,1.61094,9.5385
1080  DATA .534156,3.1257,.043633,1.977458
1090  REM  URANUS
1100  DATA .000205,2.46615,.088593,2.96706,19.182
1110  DATA  .901554,4.49084,.01396,1.28805
1120  REM  NEPTUNE
1130  DATA .000104,3.78556,.016965,.773181,30.06
1140  DATA .27054,2.233498,.031416,2.29162
1150  REM  PLUTO
1160  DATA .000069,3.16948,.471239,3.91303,39.44
1170  DATA 9.86,5.23114,.300197,1.91812
1180  FOR IP = 1 TO 10: READ P$(IP): NEXT IP
1190  DATA MERCURY,VENUS,SUN,MARS,JUPITER
1200  DATA  SATURN,URANUS,NEPTUNE,PLUTO,MOON
1210 F = 1
1220  REM  CALCULATE DATA FOR PLANETS
1230  CLS : PRINT : PRINT : PRINT 
1240  PRINT "DATA REQUESTED FOR ";YX;" ";MX;" ";DX
1250  PRINT 
1260  PRINT "-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-
1270  IF CI = 3  THEN  PRINT  TAB( 28)"R.A.  DEC"
1280  IF CI = 3  THEN  PRINT  TAB( 27)"(HRS) (DEG)": GOTO 1320
1290  PRINT "          DIST    ANG.DIST   R.A.  DEC"
1300  PRINT "       TO PLANET  FROM SUN  (HRS) (DEG)"
1310  PRINT  TAB( 11)"A.U."
1320  PRINT "-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-
1330  IF FL = AP  THEN  GOTO 1440
1340  PRINT :I = 1
1350  FOR J = 0 TO 8: GOSUB 1780
1360 A(I) = A:DS(I) = DS:L(I) = L
1370 I = I +1: NEXT J
1380  FOR I = 1 TO 9
1390  REM  SKIP EARTH
1400  IF I = 3  THEN  I = 4
1410  GOSUB 1920
1420 Q(I) = Q:X(I) = X:R(I) = R:V(I) = V
1430  NEXT I
1440 I = CI:A(I) =  FN DEG(A(I))
1450 Q$(I) =  LEFT$( STR$(Q(I)),5)
1460 X$(I) =  LEFT$( STR$(X(I)),5)
1470 R$(I) =  LEFT$( STR$(R(I)),5)
1480 V$(I) =  LEFT$( STR$(V(I)),5)
1490  IF I = 3  THEN  PRINT P$(I) TAB( 27)R$(3);
1500  IF I = 3  THEN  PRINT  TAB( 33)V$(3)
1510  IF I = 3  THEN  PRINT : GOTO 1540
1520  PRINT P$(I) TAB( 9)Q$(I) TAB( 19)X$(I) TAB( 28)R$(I);
1530  PRINT  TAB( 36)V$(I)
1540  PRINT "-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-
1550  GOSUB 2480: REM  GET RISE,TRANSIT SET
1560  PRINT : PRINT 
1570 TM = 60 *(TR - INT(TR)):TR =  INT(TR)
1580 TN = 60 *(TT - INT(TT)):TT =  INT(TT)
1590 TP = 60 *(TS - INT(TS)):TS =  INT(TS)
1600  PRINT 
1610  PRINT P$(I)"  RISES AT....... ";TR;" HR "; INT(TM);" MIN"
1620  PRINT  TAB(  LEN(P$(I)));"   TRANSITS AT.... ";TT;" HR ";
1630  PRINT  INT(TN);" MIN"
1640  PRINT  TAB(  LEN(P$(I)))"   SETS AT........ ";TS;" HR ";
1650  PRINT  INT(TP);" MIN"
1660  PRINT : PRINT 
1670  INPUT "WANT ANOTHER PLANET, SAME DATE Y/N? ";A$
1680  IF A$ = "N"  THEN  GOTO 1740
1690  IF A$ < >"Y"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1670
1700  IF CI = 10  THEN D = DX:M = MX:Y = YX: GOTO 650
1710  GOSUB 2200: IF CI = 3  THEN FL = AP
1720  IF CI = 10  THEN  GOSUB 2690: GOTO 1740
1730  GOTO 1230
1740  PRINT : INPUT "WANT ANOTHER DATE Y/N? ";A$
1750  IF A$ = "Y"  THEN FL = 0:F = 0: RESTORE : GOTO 430
1760  IF A$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1740
1770  CLS : GOTO 3200
1780  REM  CALC A,DS, AND L
1790  REM  HELIOCENTRIC LONGITUDE (A)
1800 A = NI *PD(J,0) +PD(J,1)
1810  IF A >6.28318  THEN A = ((A/6.28318) - INT(A/6.28318)) *6.28318
1820  IF A <0  THEN A = A +6.28318: GOTO 1820
1830 C = PD(J,2) * SIN(A -PD(J,3))
1840 A = A +C
1850  IF A >6.28318  THEN A = A -6.28318
1860  IF A <0  THEN A = A +6.28318: GOTO 1860
1870  REM DISTANCE OF PLANET FROM SUN (DS)
1880 DS = PD(J,4) +PD(J,5) * SIN(A -PD(J,6))
1890  REM  DISTANCE FROM ECLIPTIC (L)
1900 L = PD(J,7) * SIN(A -PD(J,8))
1910  RETURN 
1920  REM  CALC RA AND DEC
1930  REM  ANG. DIST FROM SUN (Z)
1940 Z = A(3) -A(I)
1950  IF  ABS(Z) >3.14159  AND Z <0  THEN Z = Z +6.28318
1960  IF  ABS(Z) >3.14159  AND Z >0  THEN Z = Z - -6.28318
1970  IF CI = 3  THEN X = 0: GOTO 2040
1980 Q =  SQR(DS(I) ^2 +DS(3) ^2 -2 *DS(I) *DS(3) * COS(Z))
1990 P = (DS(I) +DS(3) +Q)/2
2000 AC =  SQR(((P *(P -DS(I)))/(DS(3) *Q)))
2010  IF AC = 1  THEN AC = .999999
2020 X = 2 * FN ACO(AC)
2030  REM  R.A.
2040  IF Z <0  THEN R =  FN DEG(A(3) +3.14159 -X)/15
2050  IF Z >0  THEN R =  FN DEG(A(3) +3.14159 +X)/15
2060  IF R >24  THEN R = R -24: GOTO 2060
2070  IF R < -24  THEN R = R +24: GOTO 2070
2080  IF R <0  THEN R = R +24: GOTO 2080
2090  REM  DECLINATION
2100  IF Z <0  THEN V =  SIN(A(3) +3.14159 -X) *23.44194 + FN DEG(L(I))
2110  IF Z >0  THEN V =  SIN(A(3) +3.14159 +X) *23.44194 + FN DEG(L(I))
2120 X =  FN DEG(X)
2130 R(3) =  FN DEG(A(3) +3.14159)/15
2140  IF R(3) >24  THEN R(3) = R(3) -24: GOTO 2140
2150  IF R(3) < -24  THEN R(3) = R(3) +24: GOTO 2150
2160  IF R(3) <0  THEN R(3) = R(3) +24
2170 V(3) =  SIN(A(3) +3.14159) *23.44194
2180  RETURN 
2190  REM  PICK PLANET, SUN, OR MOON
2200  CLS : PRINT : PRINT 
2210  PRINT : PRINT : PRINT 
2220  PRINT "SELECT PLANET, SUN, OR MOON BY NUMBER"
2230  PRINT 
2240  PRINT  TAB( 8)"MERCURY.......1"
2250  PRINT  TAB( 8)"VENUS.........2"
2260  PRINT  TAB( 8)"              3....SUN"
2270  PRINT  TAB( 8)"MARS..........4"
2280  PRINT  TAB( 8)"JUPITER.......5"
2290  PRINT  TAB( 8)"SATURN........6"
2300  PRINT  TAB( 8)"URANUS........7"
2310  PRINT  TAB( 8)"NEPTUNE.......8"
2320  PRINT  TAB( 8)"PLUTO.........9"
2330  PRINT  TAB( 8)"             10....MOON"
2340  PRINT 
2350  INPUT "PLEASE SELECT NUMBER .. ";SS$
2360 SS =  VAL(SS$)
2370  IF SS <1  OR SS >10  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 2190
2380 CI = SS
2390  RETURN 
2400  REM  CALC SIDEREAL TIME T2
2410 GC = 11.927485
2420 TC = .065711
2430 T2 = TC *(NI -7020) +GC
2440  IF T2 >24  THEN T2 = T2 -24: GOTO 2440
2450  IF T2 < -24  THEN T2 = T2 +24: GOTO 2450
2460  IF T2 <0  THEN T2 = T2 +24
2470  RETURN 
2480  REM  CALC RISE, TRANSIT, AND SET TIMES
2490 TA =  TAN( FN RAD(LA)) * TAN( FN RAD(V(I)))
2500  IF TA <0  THEN TA =  ABS(TA): GOTO 2520
2510  IF TA >0  THEN TA =  -TA
2520 TA =  FN ACO(TA)
2530 TA =  FN DEG(TA)
2540 B =  -TA/15
2560 TR = (B +R(I) +TZ -T2) *.99727
2570 TR = TR -1/60
2580  IF TR <0  THEN TR = TR +24
2590  IF TR >24  THEN TR = TR -24
2600 C = TA/15
2610 TS = (C +R(I) +TZ -T2) *.99727
2620 TS = TS +3/60
2630  IF TS <0  THEN TS = TS +24
2640  IF TS >24  THEN TS = TS -24
2650 TT = R(I) -T2 +1/60
2655 TT = TT +TZ
2660  IF TT <0  THEN TT = TT +24
2670  IF TT >24  THEN TT = TT -24
2680  RETURN 
2690  REM  CALC RISE, TRANSIT, SET FOR MOON
2700  CLS : PRINT 
2710  REM  RA AND DEC OF MOON
2720 LZ = 311.1687
2730 LE = 178.699
2740 LP = 255.7433
2750 PG = .111404 *NI +LP
2760  IF PG < -360  THEN PG = PG +360: GOTO 2760
2770  IF PG <0  THEN PG = PG +360
2780  IF PG >360  THEN PG = PG -360: GOTO 2780
2790 LMD = LZ +360 *NI/27.32158
2800 PG = LMD -PG
2810 DR = 6.2886 * SIN( FN RAD(PG))
2820 LMD = LMD +DR
2830  IF LMD < -360  THEN LMD = LMD +3600: GOTO 2830
2840  IF LMD < -360  THEN LMD = LMD +360: GOTO 2840
2850  IF LMD <0  THEN LMD = LMD +360: GOTO 2850
2860  IF LMD >3600  THEN LMD = LMD -3600: GOTO 2860
2870  IF LMD >360  THEN LMD = LMD -360: GOTO 2870
2880 RM = LMD/15
2890  IF RM >24  THEN RM = RM -24: GOTO 2890
2900  IF RM <0  THEN RM = RM +24
2910 AL = LE -NI *.052954
2920  IF AL < -3600  THEN AL = AL +3600: GOTO 2920
2930  IF AL < -360  THEN AL = AL +360: GOTO 2920
2940  IF AL <0  THEN AL = AL +360: GOTO 2940
2950  IF AL >3600  THEN AL = AL -3600: GOTO 2950
2960  IF AL >360  THEN AL = AL -360: GOTO 2960
2970 AL = LMD -AL
2980  IF AL <0  THEN AL = AL +360
2990  IF AL >360  THEN AL = AL -360
3000 HE = 5.1454 * SIN(AL *3.14159/180)
3010 DM = HE +23.1444 * SIN(LMD *3.14159/180)
3020  PRINT : PRINT 
3030 RA$ =  STR$(RM):DE$ =  STR$(DM)
3040 RA$ =  LEFT$(RA$,5):DE$ =  LEFT$(DE$,5)
3050  IF  VAL(RA$) <10  THEN RA$ =  LEFT$(RA$,4)
3060  IF  VAL(DE$) < -9  THEN DE$ =  LEFT$(DE$,5): GOTO 3080
3070  IF  VAL(DE$) <10  AND  VAL(DE$) > -10  THEN DE$ =  LEFT$(DE$,4)
3080  PRINT "MOON DATA REQUESTED FOR ..."
3090  PRINT  TAB( 5)"YEAR ";YX;" MONTH ";MX;" DAY ";DX
3100  PRINT 
3110  PRINT "AT NOON"
3120  PRINT "R.A. OF MOON IS ";RA$;" HRS"
3130  PRINT "DECLINATION IS..";DE$;" DEG"
3140  PRINT "-------------------------------"
3150  REM  CALC RISE, TRANSIT, SET TIMES
3160 R(I) = RM:V(I) = DM
3170 P$(I) = "MOON"
3180  GOSUB 2480
3190  GOTO 1560
3200  RUN "D:ASTRO"
