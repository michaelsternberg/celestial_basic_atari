10 FLG = 0
20  CLS : PRINT : PRINT : PRINT 
30  PRINT : PRINT : PRINT 
40  PRINT  TAB( 12)"TIME CONVERSIONS"
50  PRINT 
60  PRINT  TAB( 10)"A COMPUTER PROGRAM"
70  PRINT  TAB( 12)"FOR ASTRONOMERS"
80  PRINT 
90  PRINT  TAB( 8)"BY ERIC BURGESS F.R.A.S."
100  PRINT 
110  PRINT  TAB( 8)"ALL RIGHTS RESERVED BY"
120  PRINT  TAB( 8)"S & T SOFTWARE SERVICE"
130  FOR J = 3000 TO 1  STEP  -1: NEXT 
140  CLS : PRINT : PRINT : PRINT 
150  REM  SET INITIAL CONDITIONS ZN TIME ZONE
160  REM  - IF EAST OF GREENWICH L$ NAME OF
170  REM  YOUR LOCALITY LO IS LONGITUDE - IF EAST
180  REM  EXAMPLE IS FOR WEST COAST
190 ZN = 8:L$ = "SEBASTOPOL":LO = 122.49
200  PRINT "INITIAL CONDITIONS SET FOR "L$
210 : PRINT : PRINT  TAB( 10)"TIME ZONE ";ZN
220  PRINT  TAB( 10)"LONGITUDE ";LO
230  PRINT 
240  INPUT "DO YOU WANT TO CHANGE CONDITIONS (Y/N) ";A$
250  IF A$ = "N"  THEN 330
260  IF A$ < >"Y"  THEN  PRINT "INVALID REPLY": PRINT : GOTO 240
270  CLS : PRINT : PRINT 
280  INPUT "LOCATION NAME ";L$
290  INPUT "TIME ZONE ";ZN$
300 ZN =  VAL(ZN$)
310  INPUT "LONGITUDE ";LO$
320 LO =  VAL(LO$)
330  CLS : PRINT : PRINT : PRINT 
340 Z = ZN
350  IF FLG = 1  THEN 820
360 A$ = "":A1$ = "":A2$ = "":A3$ = ""
370  PRINT "PROGRAM COMPUTES LOCAL MEAN TIME (LMT)"
380  PRINT 
390  PRINT "  WHEN GIVEN LOCAL SIDEREAL TIME (LST)"
400  PRINT 
410  PRINT "OR LOCAL SIDEREAL TIME WHEN GIVEN"
420  PRINT "        LOCAL MEAN TIME"
430  PRINT 
440  PRINT "AND YOU CAN REQUEST CALCULATIONS"
450  PRINT "FOR A NUMBER OF TIME INTERVALS": PRINT 
460  PRINT "TIME MAY BE ENTERED AND DISPLAYED IN"
470  PRINT "DECIMAL HOURS OR HRS. MIN. SEC"
480  PRINT "      USING 24 HOUR CLOCK": PRINT 
490  PRINT "TIME INTERVALS MUST BE IN DECIMAL HOURS"
500  PRINT : PRINT "::::::::::::::::::::::"
510  INPUT "TO CONTINUE PRESS RETURN KEY";A$
520  CLS : PRINT : PRINT : PRINT 
530  INPUT "DO YOU WANT TO CALCULATE LMT OR LST ";T$
540  PRINT : IF T$ = "LMT" GOTO 570
550  IF T$ = "LST" GOTO 570
560  PRINT "INVALID REPLY": PRINT : GOTO 530
570  PRINT "ENTER THE DATE ": PRINT 
580  INPUT "THE YEAR ";YD$:Y =  VAL(YD$)
590  IF Y = 0  THEN  PRINT "INVALID REPLY": PRINT : GOTO 580
600  IF Y >1800 GOTO 650
610  PRINT "IS ";Y;" THE CORRECT YEAR"
620  INPUT Y$
630  IF Y$ = "Y"  THEN 650
640  IF Y$ = "N"  THEN  PRINT : GOTO 570
650  PRINT : INPUT "THE MONTH ";MD$:M =  VAL(MD$)
660  IF M = 0  OR M >12  THEN  PRINT "INVALID REPLY": PRINT : GOTO 650
670  PRINT : INPUT "THE DAY ";DD$:D =  VAL(DD$)
680  IF D = 0  OR D >31  THEN  PRINT "INVALID REPLY": PRINT : GOTO 670
690  IF M = 2  AND D >29  THEN  PRINT "INVALID REPLY": PRINT : GOTO 670
700  PRINT : PRINT 
710  INPUT "WANT TO SEE LOCATION CONDITIONS? ";A$
720  IF A$ = "Y"  THEN 750
730  IF A$ = "N"  THEN 790
740  PRINT "INVALID REPLY": PRINT : GOTO 710
750  PRINT "LOCATION CONDITIONS ARE..."
760  PRINT "    LOCATION NAME... ";L$
770  PRINT "    TIME ZONE....... ";ZN
780  PRINT "    LONGITUDE....... ";LO
790  PRINT 
800  INPUT "DO YOU WANT TO CHANGE THEM ";A5$
810  IF A5$ = "Y"  THEN FLG = 1: GOTO 270
820 FLG = 0
830  GOSUB 1650
840  IF T$ = "LST"  THEN U$ = "LMT": GOTO 860
850 U$ = "LST"
860  GOSUB 1900
870  INPUT "SPECIFY NUMBER OF CALCULATIONS ";NO
880  PRINT : INPUT "AND TIME INTERVALS ";IN
890  CLS 
900  REM  CALCS FOR DAYS FROM EPOCH
910 EP = 722895: GOSUB 1950:DE = DG:ND = DE -EP
920  REM  CALC AND PRINT TIME TABLE
930  PRINT  TAB( 8)T$;" TIME(S) REQUESTED": PRINT 
940 TC = .065753
950  PRINT "DATE "Y;" ";M;" ";D
960  PRINT "LOCATION ";L$;" ZONE ";ZN -LGC/15
970  PRINT "FOR LONGITUDE ";LO
980  PRINT "INTERVAL FOR TIME CALCS ";IN;" HRS"
990  PRINT "-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-"
1000  PRINT "             ";U$;"            ";T$
1010  PRINT "   DEC HRS  HR/MI/SE  DEC HRS  HR/MI/SE"
1020  PRINT "-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-"
1030  FOR K = 1 TO NO
1040 H1 =  INT(T1):M1 =  INT((T1 -H1) *100)
1050 S1 =  INT((((T1 -H1) *100) -M1) *100)
1060  GOSUB 1230
1070  IF  INT(T2) >24  THEN T2 = T2 -24: GOTO 1080
1080 H2 =  INT(T2):M2 =  INT((T2 -H2) *100)
1090 S2 =  INT((((T2 -H2) *100) -M2) *100)
1100 HR2 =  INT(T2):MI2 = (T2 - INT(T2)) *60
1110 SE2 = (MI2 - INT(MI2)) *60:SE2 =  INT(SE2)
1120 MI2 =  INT(MI2): GOSUB 2050
1130 HA$ =  LEFT$( STR$(T1),6)
1140 :
1150 HB$ =  LEFT$( STR$(T2),6)
1160 :
1170  PRINT K; TAB( 5);HA$; TAB( 14)RR;"/";IM;"/";ES;
1180  PRINT  TAB( 23);HB$; TAB( 32)HR2;"/";MI2;"/";SE2
1190 T1 = T1 +IN
1200  IF T1 >24  THEN T1 = T1 -24:ND = ND +1: GOTO 1200
1210  NEXT K
1220  GOTO 1450
1230  REM  PARAMETERS FOR TIME CALCS
1240  REM  GMST AT EPOCH 1979 MARCH 22
1250 GC = 11.927485
1260  REM  DAILY RATE OF CHANGE OF GMST
1270 TC = .065711
1280  IF T$ = "LMT"  THEN 1370
1290  REM  CONVERSION TO LST
1300 T2 = TC *ND +GC +(((ZN +T1)/24) *TC) +T1
1310  REM  CONVERSION FOR NON MID TIME ZONE
1320 T2 = T2 +(.0656667 *LGC)
1330  IF T2 >24  THEN T2 = T2 -24: GOTO 1330
1340  IF T2 < -24  THEN T2 = T2 +24: GOTO 1340
1350  IF T2 <0  THEN T2 = T2 +24
1360  RETURN 
1370  REM  CONVERSION TO LMT
1380 T2 = T1 -(TC *ND +GC) -(ZN/24 *TC)
1390  REM  CONVERSION TO NON MID TIME ZONE
1400 T2 = T2 -(.0656667 *LGC)
1410  IF T2 <0  THEN T2 = T2 +24: GOTO 1410
1420  IF T2 >24  THEN T2 = T2 -24: GOTO 1420
1430 T2 = T2 -(T2/24 *TC)
1440  RETURN 
1450  PRINT "-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-"
1460  INPUT "DO YOU WANT MORE CALCULATIONS ";A$
1470  CLS : PRINT : PRINT : PRINT 
1480  IF A$ = "N"  THEN  GOTO 1630
1490  IF A$ < >"Y"  THEN  PRINT "INVALID REPLY": PRINT : GOTO 1460
1500 H1$ = "":M1$ = "":S1$ = "":HA$ = "":H2$ = "":M2$ = "":S2$ = "":HB$ = ""
1510 T1 = 0:T2 = 0
1520  CLS : PRINT : PRINT : PRINT 
1530  PRINT "DO YOU STILL WANT TO CALCULATE ";T$
1540  INPUT A$
1550  IF A$ = "N"  AND T$ = "LST"  THEN T$ = "LMT": GOTO 1580
1560  IF A$ = "N"  AND T$ = "LMT"  THEN T$ = "LST": GOTO 1580
1570  IF A$ < >"Y"  THEN  PRINT "INVALID REPLY": PRINT : GOTO 1530
1580  CLS : PRINT : PRINT : PRINT 
1590  INPUT "DO YOU WANT TO CHANGE DATE ";A$
1600  IF A$ = "N"  THEN ZN = Z: GOTO 700
1610  IF A$ < >"Y"  THEN  PRINT "INVALID REPLY": PRINT : GOTO 1590
1620  PRINT :ZN = Z: GOTO 570
1630  CLS 
1640  RUN "D:ASTRO"
1650  CLS : PRINT : PRINT : PRINT 
1660  PRINT "DO YOU WANT INPUT IN DEC. HRS (1)"
1670  PRINT "        OR IN HRS MIN SEC (2)"
1680  PRINT : PRINT 
1690  PRINT  TAB( 10)" ";: INPUT PT$
1700 PT =  VAL(PT$): PRINT : PRINT 
1710  IF PT = 1  THEN  GOTO 1750
1720  IF PT = 2  THEN  GOTO 1820
1730  PRINT "INVALID REPLY": PRINT : GOTO 1650
1740  IF T$ = "LMT"  THEN T$ = "LST"
1750  PRINT "    WHAT IS THE INPUT TIME"
1760  PRINT "    HR.XXXX (24-HR CLOCK)"
1770  PRINT : PRINT 
1780  PRINT "    ";: INPUT T1: PRINT 
1790  IF T1 >23.9999  THEN  PRINT : PRINT "INVALID ENTRY": GOTO 1780
1800  PRINT : PRINT : PRINT 
1810  GOTO 1890
1820  PRINT "   WHAT IS THE INPUT TIME"
1830  PRINT "   HR,MIN,SEC (24-HR CLOCK)"
1840  PRINT : PRINT 
1850  PRINT "     ";: INPUT HR,MI,SE
1860  PRINT : PRINT : PRINT 
1870  IF HR >23  OR MI >59  OR SE >59.99  THEN  PRINT "INVALID ENTRY": PRINT : GOTO 1850
1880 T1 = HR +MI/60 +SE/3600
1890  RETURN 
1900 LGC = (ZN *15) -LO
1910  IF LGC <0  THEN  GOTO 1930
1920 ZN = ZN + ABS(LGC/15): GOTO 1940
1930 ZN = ZN +LGC/15
1940  RETURN 
1950  REM  CALC GREGORIAN DAYS TO DATE REQUESTED
1960  IF M > = 3 GOTO 2020
1970  REM  CALCS FOR JAN AND FEB
1980 DG = 365 *Y +D
1990 DG = DG +((M -1) *31) + INT((Y -1)/4) - INT((.75) * INT((Y -1)/100 +1))
2000  RETURN 
2010  REM  CALCS FOR MAR THRU DEC
2020 DG = 365 *Y +D +((M -1) *31) - INT(M *.4 +2.3)
2030 DG = DG + INT(Y/4) - INT((.75) * INT((Y/100) +1))
2040  RETURN 
2050  REM  SUB FOR PRINTING HR,MI,SE
2060 RR =  INT(T1)
2070 IM = (T1 - INT(T1)) *60:ES = (IM - INT(IM)) *60
2080 ES =  INT(ES):IM =  INT(IM)
2090  RETURN 
