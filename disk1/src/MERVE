10  CLS 
20  DEF  FN ACO(X) =  - ATN(X/ SQR( -X *X +1)) +1.570793
30 P1 = 3.14159:P2 = 6.28318
40  PRINT : PRINT 
50  PRINT  TAB( 5)"--------------------------------"
60  PRINT  TAB( 5)"I       MERCURY AND VENUS      I"
70  PRINT  TAB( 5)"--------------------------------"
80  PRINT : PRINT 
90  PRINT  TAB( 11)"AN ASTRONOMY PROGRAM": PRINT : PRINT 
100  PRINT  TAB( 10)"BY ERIC BURGESS F.R.A.S.": PRINT : PRINT 
110  PRINT  TAB( 11)"ALL RIGHTS RESERVED BY"
120  PRINT  TAB( 11)"S $ T SOFTWARE SERVICE"
130  PRINT : PRINT 
140  PRINT  TAB( 13)"JAN. 82 VERSION": PRINT : PRINT 
150  PRINT  TAB( 5)"";
160  INPUT "DO YOU WANT INSTRUCTIONS? Y/N ";A$
170  IF A$ = "N"  THEN  CLS : PRINT : PRINT : PRINT : GOTO 320
180  IF A$ < >"Y"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 160
190  CLS : PRINT : PRINT : PRINT 
200  PRINT : PRINT : PRINT 
210  PRINT  TAB( 5)"THIS PROGRAM PLOTS THE POSITIONS"
220  PRINT  TAB( 5)"OF MERCURY AND VENUS RELATIVE"
230  PRINT  TAB( 5)"TO THE SUN AT INTERVALS CHOSEN BY"
240  PRINT  TAB( 5)"YOU IN UNITS OF DAYS FOR AS MANY"
250  PRINT  TAB( 5)"INTERVALS AS YOU REQUEST."
260  PRINT : PRINT 
270  PRINT  TAB( 5)"YOU CAN THEN ASK TO SEE DATA ABOUT"
280  PRINT  TAB( 5)"EITHER OR BOTH PLANETS, SUCH AS"
290  PRINT  TAB( 5)"DISTANCE, APPARENT SIZE,& PHASE."
300  PRINT : PRINT : PRINT 
310  INPUT "TO CONTINUE PRESS RETURN ";A$
320 FL = 0
330  CLS : PRINT : PRINT : PRINT 
340 F9 = 0:NT = 1
350  INPUT "SELECT INTERVAL (DAYS) ";ND
360  PRINT : PRINT : PRINT 
370  INPUT "SCROLLED DISPLAY? Y/N ";FL$
380  IF FL$ = "N"  THEN F9 = 1
390  PRINT : PRINT 
400  INPUT "HOW MANY INTERVALS? ";NS
410  GOTO 2200
420  GOSUB 990: REM  GET DISTANCE FROM EARTH
430  CLS : PRINT : PRINT 
440  GOSUB 450: GOTO 930
450  CLS : PRINT : PRINT 
460 MD$ =  LEFT$( STR$(6.68/MQ),5)
470 VD$ =  LEFT$( STR$(16.82/VQ),5)
480  PRINT "FOR YEAR ";Y;" MONTH ";M;" DAY ";D: PRINT 
490  PRINT "---------------------------": PRINT 
500 MQ$ =  LEFT$( STR$(MQ),4)
510 VQ$ =  LEFT$( STR$(VQ),4)
520  IF E$ = "2"  THEN  GOTO 560
530  PRINT "DISTANCE OF MERCURY IS ";MQ$;" A.U."
540  PRINT  TAB( 15)"OR "; VAL( LEFT$( STR$(MQ *92.9),5));" MILLION MILES"
550  IF E$ = "1"  THEN  GOTO 580
560  PRINT "DISTANCE OF VENUS IS ";VQ$;" A.U."
570  PRINT  TAB( 15)"OR "; VAL( LEFT$( STR$(VQ *92.9),5));" MILLION MILES"
580  PRINT 
590 MW$ =  LEFT$( STR$(MW *57.29578),4)
600 VW$ =  LEFT$( STR$(VW *57.29578),4)
610  IF E$ = "2"  THEN  GOTO 670
620  PRINT "MERCURY ANG.DIST. FROM SUN IS ";MW$;
630  PRINT " DEG"
640  GOSUB 2710
650  PRINT  TAB( 2)"(LOCATED ";AP$;" FOR VIEWING)"
660  IF E$ = "1"  THEN  PRINT : GOTO 700
670  PRINT "ANG. DIST. OF VENUS FROM SUN IS ";VW$;
680  PRINT " DEG"
690  IF E$ = "3"  OR  VAL(SD$) = 3  THEN  INPUT "PRESS RETURN TO CONTINUE ";A$: CLS : PRINT 
700  IF E$ = "2"  THEN  GOTO 760
710  PRINT "ANG. DIAM. OF MERCURY IS ";MD$;
720  PRINT " ARCSEC"
730  PRINT " (AT INFER.CONJ. DIAM. IS 12 ARCSEC)"
740  IF E$ = "1"  THEN  GOTO 790
750  PRINT 
760  PRINT "ANG. DIAM. OF VENUS IS ";VD$;
770  PRINT " ARCSEC"
780  PRINT "  (AT INFER. CONJ. DIAM. IS 61 ARCSEC)"
790  PRINT 
800 PH =  ABS(MW) + ABS(MZ): GOSUB 2520
810  IF E$ = "2"  THEN  GOTO 860
820  PRINT "MERCURY'S PHASE IS ";PH$
830  PRINT  TAB( 5)PH;" DEG. ILLUMINATED"
840  PRINT 
850  IF E$ = "1"  THEN  GOTO 890
860 PH =  ABS(VW) + ABS(ZV): GOSUB 2520
870  PRINT "PHASE OF VENUS IS ";PH$
880  PRINT  TAB( 5);PH;" DEG. ILLUMINATED"
890  PRINT 
900  GOSUB 1920
910  GOSUB 2100
920  RETURN 
930  PRINT : INPUT "WANT ANOTHER DATE? ";A$
940  IF A$ = "N"  THEN  CLS : GOTO 980
950  IF A$ < >"Y"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 930
960  CLS : PRINT : PRINT : PRINT 
970  GOTO 320
980  RUN "D:ASTRO"
990  REM  FIND ANGULAR DIAM
1000  CLS : PRINT : PRINT : PRINT 
1010  GOSUB 1290: REM  ENTER DATE
1020  GOSUB 1480: REM  CALC DAYS FROM EPOCH
1030  REM  CALC DIST. FROM EARTH
1040  REM  AND ANG. DIST. FROM SUN
1050 AE = NI *.017202 +1.74022
1060  IF AE >P2  THEN AE = ((AE/P2) - INT(AE/P2)) *P2
1070  IF AE <0  THEN AE = AE +P2: GOTO 1070
1080 CE = .032044 * SIN(AE -1.78547)
1090 AE = AE +CE
1100  IF AE >P2  THEN AE = AE -P2
1110  IF AE <0  THEN AE = AE +P2: GOTO 1110
1120 DE = 1 +.017 * SIN(AE -3.33926)
1130  GOSUB 1590: REM  CALC MERCURY'S POSITION
1140 MZ = AE -MA
1150 ZM = MZ
1160  IF  ABS(MZ) >P1  AND MZ <0  THEN MZ = MZ +P2
1170  IF  ABS(MZ) >P1  AND MZ >0  THEN MZ = MZ -P2
1180  REM  CALC DIST FROM EARTH
1190 MQ =  SQR(MD ^2 +DE ^2 -2 *MD *DE * COS(MZ))
1200 MP = (MD +DE +MQ)/2
1210  REM  CALC ANG DIST. FROM SUN
1220 MV =  SQR((MP *(MP -MD))/(DE *MQ))
1230 MW = 2 * FN ACO(MV)
1240  IF  ABS(ZM) >P1  AND ZM <0  THEN ZM = ZM +P2
1250  IF  ABS(ZM) >P1  AND ZM >0  THEN ZM = ZM -P2
1260  IF ZM >0  THEN MW =  -MW
1270  GOSUB 1690: REM  CALC VENUS'S POSITION
1280  RETURN 
1290  REM  ENTER DATE
1300  PRINT : PRINT 
1310  PRINT "ENTER THE DATE": PRINT 
1320  INPUT "   THE YEAR ";Y$:Y =  VAL(Y$)
1330  IF Y = 0  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1320
1340  IF Y >1800  THEN  GOTO 1390
1350  PRINT " ";Y;" THE CORRECT YEAR? ": INPUT A$
1360  IF A$ = "Y"  THEN  GOTO 1390
1370  IF A$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1350
1380  IF A$ = "N"  THEN  GOTO 1320
1390  PRINT : INPUT "   THE MONTH ";M$:M =  VAL(M$)
1400  IF M = 0  OR M >12  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1390
1410  PRINT : INPUT "   THE DAY ";D$:D =  VAL(D$)
1420  IF D = 0  OR D >31  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1410
1430  IF M = 2  AND D >29  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1410
1440  CLS 
1450  REM  STORE INITIAL DATE
1460 YI = Y:MI = M:DI = D
1470  RETURN 
1480  REM  CALC DAYS FROM EPOCH
1490 DG = 365 *Y +D +((M -1) *31)
1500  IF M > = 3  THEN  GOTO 1540
1510  REM  CALC FOR JAN OR FEB
1520 DG = DG + INT((Y -1)/4) - INT((.75) * INT((Y -1)/100 +1))
1530  GOTO 1560
1540  REM  CALC FOR MAR THRU DEC
1550 DG = DG - INT(M *.4 +2.3) + INT(Y/4) - INT((.75) * INT((Y/100) +1))
1560 NI = DG -715875
1570  RETURN 
1580  GOTO 420
1590  REM  CALC MERCURY'S POSITION
1600 MA = NI *.071425 +3.8494
1610  IF MA >P2  THEN MA = ((MA/P2) - INT(MA/P2)) *P2
1620  IF MA <0  THEN MA = MA +P2: GOTO 1620
1630 MC = .388301 * SIN(MA -1.34041)
1640 MA = MA +MC
1650  IF MA >P2  THEN MA = MA -P2
1660  IF MA <0  THEN MA = MA +P2: GOTO 1660
1670 MD = .3871 +.079744 * SIN(MA -2.73514)
1680  RETURN 
1690  REM  CALC VENUS'S POSITION
1700 VA = NI *.027962 +3.02812
1710  IF VA >P2  THEN VA = ((VA/P2) - INT(VA/P2)) *P2
1720  IF VA <0  THEN VA = VA +P2: GOTO 1720
1730 VC = .013195 * SIN(VA -2.28638)
1740 VA = VA +VC
1750  IF VA >P2  THEN VA = VA -P2
1760  IF VA <0  THEN VA = VA +P2: GOTO 1760
1770 VD = .7233 +.00506 * SIN(VA -3.85017)
1780 ZV = AE -VA
1790 VZ = ZV
1800  IF  ABS(ZV) >P1  AND ZV <0  THEN ZV = ZV +P2
1810  IF  ABS(ZV) >P1  AND ZV >0  THEN ZV = ZV -P2
1820  REM  CALC DIST FROM EARTH
1830 VQ =  SQR(VD ^2 +DE ^2 -2 *VD *DE * COS(ZV))
1840 VP = (VD +DE +VQ)/2
1850  REM  CALC ANG DIST FROM SUN
1860 VV =  SQR((VP *(VP -VD))/(DE *VQ))
1870 VW = 2 * FN ACO(VV)
1880  IF  ABS(VZ) >P1  AND VZ <0  THEN VZ = VZ +P2
1890  IF  ABS(VZ) >P1  AND VZ >0  THEN VZ = VZ -P2
1900  IF VZ >0  THEN VW =  -VW
1910  RETURN 
1920  REM  SHOW SUN AND PLANETS
1930 Z$(0) = MW$ +"M":Z$(1) = VW$ +"V"
1940 Z$(2) =  STR$(0) +"O"
1950  FOR I = 0 TO 2
1960 U$(I) = "999"
1970  NEXT I
1980 K = 0
1990  FOR I = 0 TO 2
2000  IF Z$(I) = "999"  THEN  GOTO 2080
2010  FOR J = I TO 2
2020  IF Z$(J) = "999"  THEN  GOTO 2040
2030  IF  VAL(Z$(J)) < VAL(Z$(I))  THEN I = J: GOTO 2040
2040  NEXT J
2050 U$(K) = Z$(I):Z$(I) = "999"
2060 K = K +1
2070 I =  -1
2080  NEXT I
2090  RETURN 
2100  PRINT "VISUAL CONFIG. RELATIVE TO SUN"
2110  PRINT 
2120 M1 = 20 +.8 * VAL( LEFT$(U$(0),5))/2
2130  PRINT  TAB( M1); RIGHT$(U$(0),1);
2140 M2 = 20 +.8 * VAL( LEFT$(U$(1),5))/2
2150  PRINT  TAB( M2); RIGHT$(U$(1),1);
2160 M3 = 20 +.8 * VAL( LEFT$(U$(2),5))/2
2170  PRINT  TAB( M3); RIGHT$(U$(2),1)
2180  PRINT 
2190  RETURN 
2200  CLS : REM  ROUTINE FOR ELONGATIONS
2210  GOSUB 2220
2220  CLS : GOSUB 1290: REM   GET DATE
2230 FL = 0
2240  GOSUB 1480: REM  CALC DAYS FROM EPOCH
2250 :
2260  GOSUB 1030:VW$ =  STR$(VW *57.29578)
2270 MW$ =  STR$(MW *57.29578): GOSUB 1920
2280  IF F9 = 1  THEN  CLS : PRINT : PRINT : PRINT 
2290  PRINT "DISPLAY ";NT;" IS DATE ";YI;" ";MI;" ";DI: PRINT : GOSUB 2110
2300  IF NT = NS  THEN  PRINT : PRINT 
2310  IF NT = NS  THEN  GOSUB 2800
2320  IF NT = NS  THEN  CLS : GOTO 2390
2330  PRINT 
2340 D = D +ND
2350  GOSUB 2610
2360 YI = Y:DI = D:MI = M
2370 FL = 1:NT = NT +1
2380  GOTO 2240
2390  GOSUB 450
2400  IF SD$ = "3"  THEN E$ = "":SD$ = "": PRINT : GOTO 2480
2410  IF OP = 1  THEN OP = 0: GOTO 2480
2420  INPUT "WANT THE OTHER PLANET? Y/N ";A$
2430  IF A$ = "Y"  AND E$ = "1"  THEN E$ = "2":A$ = "": GOSUB 450: GOTO 2460
2440  IF A$ = "Y"  AND E$ = "2"  THEN E$ = "1":A$ = "": GOSUB 450: GOTO 2460
2450  IF A$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 2420
2460  PRINT : INPUT "WANT BOTH PLANETS? Y/N ";A$
2470  IF A$ = "Y"  THEN E$ = "3":OP = 1: GOSUB 450:E$ = "":OP = 0
2480  INPUT "DO YOU WANT ANOTHER DATE? Y/N ";A$
2490  IF A$ = "Y"  THEN  GOTO 320
2500  IF A$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 2480
2510  CLS : GOTO 980
2520  REM  SUB FOR PHASES
2530 PH = 180 -PH *57.2958
2540  IF PH >150  THEN PH$ = "THIN CRESCENT"
2550  IF PH >120  AND PH <151  THEN PH$ = "FAT CRESCENT"
2560  IF PH >70  AND PH <121  THEN PH$ = "NEAR HALF"
2570  IF PH >29  AND PH <71  THEN PH$ = "GIBBOUS"
2580  IF PH <30  THEN PH$ = "NEAR FULL"
2590 PH =  INT(180 -PH)
2600  RETURN 
2610  REM  SUB FOR MONTH END ADJUSTS
2620  IF Y/4 - INT(Y/4) = 0  AND Y/100 - INT(Y/100) < >0  THEN LY = 1
2625  IF Y/400 - INT(Y/400) = 0  THEN LY = 1
2630  IF M >12  THEN M = M -12:Y = Y +1
2640  IF LY = 1  AND M = 2  AND D >29  THEN M = 3:D = D -29: GOTO 2660
2650  IF LY = 0  AND M = 2  AND D >28  THEN M = 3:D = D -28: GOTO 2660
2660  IF D <31 GOTO 2700
2670  IF (M = 4  OR M = 6  OR M = 9  OR M = 11)  THEN M = M +1:D = D -30: GOTO 2630
2680  IF D >31  THEN M = M +1:D = D -31: GOTO 2630
2690  IF M >12  THEN M = M -12:Y = Y +1
2700  RETURN 
2710  REM  SUB FOR VIEWING MERCURY
2720 MX =  VAL(MW$)
2730 AP$ = ""
2740  IF MX >13  AND M >7  AND M <12  THEN AP$ = "FAVORABLY": GOTO 2790
2750  IF MX >18  AND M >7  AND M <12  THEN AP$ = "VERY FAVORABLY": GOTO 2790
2760  IF MX < -13  AND M >1  AND M <7  THEN AP$ = "FAVORABLY": GOTO 2790
2770  IF MX < -18  AND M >1  AND M <7  THEN AP$ = "VERY FAVORABLY": GOTO 2790
2780 AP$ = "UNFAVORABLY"
2790  RETURN 
2800  PRINT "SELECT DETAILS FOR DISPLAY"
2810  INPUT "MERCURY (1); VENUS (2); OR BOTH (3) ";SD$
2820  IF  VAL(SD$) = 3  THEN  RETURN 
2830 E$ = SD$: RETURN 
2840  PRINT "INVALID RESPONSE": PRINT : GOTO 2800
