10  REM  GREETING-1 PROGRAM
20  REM  RA AND DEC OF MOON FOR ANY DATE
30  DEF  FN RAD(X) = .01745328(X)
40  CLS : PRINT : PRINT : PRINT : PRINT 
45  POKE 82,0
50  PRINT  TAB( 9)"ASTRONOMY PROGRAM": PRINT 
60  PRINT  TAB( 9)"-----------------"
70  PRINT  TAB( 9)"I     RADEM     I"
80  PRINT  TAB( 9)"-----------------"
90  PRINT 
100  PRINT  TAB( 7)"BY ERIC BURGESS F.R.A.S."
110  PRINT : PRINT 
120 FL = 0
130  PRINT  TAB( 7)"ALL RIGHTS RESERVED BY"
140  PRINT  TAB( 7)"S & T SOFTWARE SERVICE"
150  PRINT 
160  PRINT  TAB( 10)"VERSION 4/82"
170  PRINT : PRINT : PRINT 
180  INPUT "DO YOU WANT INSTRUCTIONS Y/N ";A$
190  IF A$ = "N"  THEN  CLS : PRINT : PRINT : GOTO 390
200  IF A$ < >"Y"  THEN  PRINT : PRINT "INVALID RESPONSE": GOTO 180
210  CLS : PRINT : PRINT : PRINT 
220  PRINT "THIS PROGRAM DISPLAYS THE APPROXIMATE"
230  PRINT "RIGHT ASCENSION AND DECLINATION"
240  PRINT "OF THE MOON FOR A REQUESTED DATE"
250  PRINT  TAB( 12)"AND TIME": PRINT 
260  PRINT "FOR A SERIES OF INTERVALS"
270  PRINT : PRINT 
280  PRINT "ENTER YEAR, MONTH, DAY, TIME ZONE,"
290  PRINT "      AND LOCAL TIME"
300  PRINT "ALSO TIME INTERVALS IN DAYS AND THE"
310  PRINT "NUMBERS OF INTERVALS REQUIRED"
320  PRINT : PRINT 
330  PRINT "THE PROGRAM WILL DISPLAY UNIVERSAL TIME"
340  PRINT "AND THE APPROXIMATE RA AND DECLINATION"
350  PRINT "FOR EACH DAY AT THE SELECTED INTERVALS"
360  PRINT : PRINT : PRINT 
370  INPUT "WHEN READY PRESS RETURN";A$
380  CLS : PRINT : PRINT : PRINT 
390  PRINT "ENTER THE DATE": PRINT 
400  INPUT "THE YEAR ";YD$:Y =  VAL(YD$)
410  IF Y = 0  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 400
420  IF Y >1800 GOTO 480
430  PRINT "IS ";Y;" THE CORRECT YEAR?"
440  INPUT Y$
450  IF Y$ = "Y"  THEN 480
460  IF Y$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 430
470  IF Y$ = "N"  THEN  PRINT : GOTO 400
480 LY = 0
490  IF Y/4 - INT(Y/4) = 0  AND Y/100 - INT(Y/100) < >0  THEN LY = 1
495  IF Y/400 - INT(Y/400) = 0  THEN LY = 1
500  PRINT : INPUT "THE MONTH ";MD$:M =  VAL(MD$)
510  IF M = 0  OR M >12  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 500
520  PRINT : INPUT "THE DAY ";DD$:D =  VAL(DD$)
530  IF D = 0  OR D >31  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 520
540  IF M = 2  AND D >29  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 520
550  IF LY < >1  AND M = 2  AND D >28  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 520
560  PRINT : INPUT "TIME ZONE ";TZ
570  PRINT : INPUT "TIME HRS ";T1
580  REM  STORE INITIAL DATE AND TIME
590 YP = Y:MP = M:DP = D
600 DU = D:TU = TZ +T1: IF TU >24  THEN TU = TU -24:DU = DU +1
610 DU = DU +TU/24
620  IF (LY = 1  AND M = 2  AND  INT(DU) >29)  THEN M = 3:DU = DU -29: GOTO 670
630  IF (LY = 0  AND M = 2  AND  INT(DU) >28)  THEN M = 3:DU = DU -28: GOTO 670
640  IF DU <31 GOTO 670
650  IF (M = 4  OR M = 6  OR M = 9  OR M = 11)  THEN M = M +1:DU = DU -30: GOTO 670
660  IF  INT(DU) >31  THEN M = M +1:DU = DU -31
670  IF M = 13  THEN M = 1:Y = Y +1
680  IF F9 = 1  THEN 710
690 T2 = TU:D2 = DU:Y2 = Y:M2 = M
700  IF FL = 5  THEN  GOTO 860
710  PRINT : PRINT : PRINT 
720  PRINT "SELECT INTERVALS AND HOW MANY"
730  PRINT "ENTER 1'S IF YOU NEED ONE PLOT ONLY"
740  PRINT "UP TO THREE INTERVALS ARE DISPLAYED"
750  PRINT "ON SCREEN BEFORE SCROLLING"
760  PRINT : PRINT 
770  INPUT "WHAT IS THE TIME INTERVAL (DAYS) ";TI$
780  PRINT 
790 TI =  VAL(TI$)
800  IF TI = 0  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 770
810  INPUT "HOW MANY INTERVALS ";IN$: PRINT 
820 IN =  VAL(IN$)
830  IF IN = 0  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 810
840  REM  SETS INTERVAL COUNT AT 1
850 NC = 1
860  REM  CALC DAYS FROM EPOCH 1960,1,1
870  IF NC >1 GOTO 900
880  CLS : PRINT 
890  PRINT "INTERVALS OF ";TI;" DAYS FOR TIME ZONE ";TZ
900 DG = 365 *Y +DU +((M -1) *31)
910  IF M > = 3 GOTO 950
920  REM  CALC IF JAN OR FEB
930 DG = DG + INT((Y -1)/4) - INT((.75) * INT((Y -1)/100 +1))
940  GOTO 970
950  REM  CALC FOR MAR THRU DEC
960 DG = DG - INT(M *.4 +2.3) + INT(Y/4) - INT((.75) * INT((Y/100) +1))
970 NM = DG -715875
980 NM = NM -.5
990  REM  FIND RA AND DEC OF MOON
1000  REM  LONG OF MOON
1010 LZ = 311.1687
1020 LE = 178.699
1030 LP = 255.7433
1040 PG = .111404 *NM +LP
1050  IF PG < -360  THEN PG = PG +360: GOTO 1050
1060  IF PG <0  THEN PG = PG +360
1070  IF PG >360  THEN PG = PG -360: GOTO 1070
1080 LMD = LZ +360 *NM/27.321582
1090 PG = LMD -PG
1100 DR = 6.2886 * SIN(.01745328 *PG)
1110 LMD = LMD +DR
1120  IF LMD < -3600  THEN LMD = LMD +3600: GOTO 1120
1130  IF LMD < -360  THEN LMD = LMD +360: GOTO 1130
1140  IF LMD <0  THEN LMD = LMD +360: GOTO 1140
1150  IF LMD >3600  THEN LMD = LMD -3600: GOTO 1150
1160  IF LMD >360  THEN LMD = LMD -360: GOTO 1160
1170 RM = LMD/15
1180  IF RM >24  THEN RM = RM -24: GOTO 1180
1190  IF RM <0  THEN RM = RM +24
1200 AL = LE -NM *.052954
1210  IF AL < -3600  THEN AL = AL +3600: GOTO 1210
1220  IF AL < -360  THEN AL = AL +360: GOTO 1220
1230  IF AL <0  THEN AL = AL +360: GOTO 1230
1240  IF AL >3600  THEN AL = AL -3600: GOTO 1240
1250  IF AL >360  THEN AL = AL -360: GOTO 1250
1260 AL = LMD -AL
1270  IF AL <0  THEN AL = AL +360
1280  IF AL >360  THEN AL = AL -360
1290 HE = 5.1454 * SIN(AL *3.14159/180)
1300 DM = HE +23.1444 * SIN(LMD *3.14159/180)
1310  PRINT 
1320 RA$ =  STR$(RM):DE$ =  STR$(DM)
1330 RA$ =  LEFT$(RA$,5):DE$ =  LEFT$(DE$,5)
1340  IF  VAL(RA$) <10  THEN RA$ =  LEFT$(RA$,4)
1350  IF  VAL(DE$) < -9  THEN DE$ =  LEFT$(DE$,5): GOTO 1370
1360  IF  VAL(DE$) <10  AND  VAL(DE$) > -10  THEN DE$ =  LEFT$(DE$,4)
1370  PRINT "LOCL TIME ";T1;" HR:YR ";YP;" MNTH ";MP;" DY ";DP
1380  PRINT "UNIV TIME ";TU;" HR:YR ";Y2;" MNTH ";M2;" DY "; INT(D2)
1390  PRINT 
1400  PRINT "RA OF MOON IS ";RA$;" DECLINATION IS ";DE$
1410  PRINT "------------------------------"
1420  IF NC/3 - INT(NC/3) = 0  THEN  INPUT "PRESS RETURN TO CONTINUE";Z$
1430  IF NC <IN GOTO 1450
1440  GOTO 1550
1450 NC = NC +1:D = DP +TI
1460 Y = YP:M = MP
1470  IF (LY = 1  AND M = 2  AND D >29)  THEN M = 3:D = D -29: GOTO 1530
1480  IF (LY = 0  AND M = 2  AND D >28)  THEN M = 3:D = D -28: GOTO 1530
1490  IF D <31 GOTO 1530
1500  IF (M = 4  OR M = 6  OR M = 9  OR M = 11)  THEN M = M +1:D = D -30: GOTO 1530
1510  IF D >31  THEN M = M +1:D = D -31
1520  IF M = 2  THEN  GOTO 1470
1530  IF M = 13  THEN M = 1:Y = Y +1
1540 FL = 5: GOTO 590
1550  PRINT 
1560  INPUT "DO YOU WANT ANOTHER DATE Y/N ";A$
1570  IF A$ = "N"  THEN  CLS : GOTO 1610
1580  IF A$ < >"Y"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1560
1590 FL = 0
1600  CLS : PRINT : PRINT : GOTO 390
1610  RUN "D:ASTRO"
