10  REM  RA AND DEC OF ALL PLANETS
20  DEF  FN ASN(X) =  ATN(X/ SQR( -X *X +1))
30  DEF  FN ACO(X) =  - ATN(X/ SQR( -X *X +1)) +1.5707963
40  DEF  FN RAD(X) = .01745328 *(X)
50  DEF  FN DEG(X) = 57.29578 *(X)
60  CLS : PRINT : PRINT : PRINT : PRINT 
70  PRINT  TAB( 11)"ASTRONOMY PROGRAM"
80  PRINT : PRINT 
90  PRINT  TAB( 11)"-----------------"
100  PRINT  TAB( 11)"I     RADEC     I"
110  PRINT  TAB( 11)"-----------------"
120  PRINT : PRINT 
130  PRINT  TAB( 8)"BY ERIC BURGESS F.R.A.S."
140  PRINT : PRINT 
150  PRINT  TAB( 9)"ALL RIGHTS RESERVED BY"
160  PRINT  TAB( 9)"S & T SOFTWARE SERVICE"
165  PRINT : PRINT  TAB( 12);"VERSION JUNE 1983": PRINT 
170  FOR J = 3000 TO 1  STEP  -1: NEXT 
180  CLS : PRINT : PRINT : PRINT 
190  PRINT : PRINT : PRINT : PRINT 
200  PRINT  TAB( 2)"THIS PROGRAM GIVES RIGHT ASCENSION"
210  PRINT  TAB( 11)"AND DECLINATIONS"
220  PRINT  TAB( 3)"HELIOCENTRIC LONGITUDES AND THE"
230  PRINT  TAB( 3)"DISTANCES FROM EARTH OF ALL THE"
240  PRINT  TAB( 12)"PLANETS": PRINT : PRINT 
250  PRINT  TAB( 4)"FOR THE DATE WHICH YOU INPUT"
260  PRINT : PRINT : PRINT : PRINT 
270  INPUT "TO CONTINUE PRESS RETURN";A$
280  CLS : PRINT : PRINT : PRINT : PRINT 
290  PRINT "ENTER THE DATE": PRINT 
300 FL = 2
310  INPUT "THE YEAR ";YD$:Y =  VAL(YD$)
320  IF Y = 0  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 310
330  IF Y >1800 GOTO 370
340  PRINT "IS ";Y;" THE CORRECT YEAR? ": INPUT Y$
350  IF Y$ = "Y"  THEN 370
360  IF Y$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 340
370  PRINT : INPUT "THE MONTH ";MD$:M =  VAL(MD$)
380  IF M = 0  OR M >12  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 370
390  PRINT : INPUT "THE DAY ";DD$:D =  VAL(DD$)
400  IF D = 0  OR D >31  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 390
410  IF M = 2  AND D >29  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 390
420  REM  CALC GREG. DAYS TO DATE
430  REM  FROM EPOCH 1960,1,1
440 DG = 365 *Y +D +((M -1) *31)
450  IF M > = 3 GOTO 490
460  REM  CALC FOR JAN AND FEB
470 DG = DG + INT((Y -1)/4) - INT((.75) * INT((Y -1)/100 +1))
480  GOTO 510
490  REM  CALC FOR MAR THRU DEC
500 DG = DG - INT(M *.4 +2.3) + INT(Y/4) - INT((.75) * INT((Y/100) +1))
510 NI = DG -715875
520  REM  PLANETARY DATA FOR EPOCH 1960
530  IF F = 1 GOTO 790
540  RESTORE 
550  DIM PD(9,10)
560  FOR YY = 0 TO 8: FOR XX = 0 TO 8
570  READ PD(YY,XX)
580  NEXT XX,YY
590  REM  MERCURY
600  DATA .071422,3.8484,.388301,1.34041,.3871,.07974,2.73514,.122173,.836013
610  REM  VENUS
620  DATA .027962,3.02812,.013195,2.28638,.7233,.00506,3.85017,.059341,1.33168
630  REM  EARTH
640  DATA .017202,1.74022,.032044,1.78547,1,.017,3.33929,0,0
650  REM  MARS
660  DATA .009146,4.51234,.175301,5.85209,1.5237,.141704,1.04656,.03142,.858702
670  REM  JUPITER
680  DATA  .00145,4.53364,.090478,.23911,5.2028,.249374,1.76188,.01972,1.74533
690  REM  SATURN
700  DATA .000584,4.89884,.105558,1.61094,9.5385,.534156,3.1257,.043633,1.977458
710  REM  URANUS
720  DATA .000205,2.46615,.088593,2.96706,19.182,.901554,4.49084,.01396,1.28805
730  REM  NEPTUNE
740  DATA .000104,3.78556,.016965,.773181,30.06,.27054,2.33498,.031416,2.29162
750  REM  PLUTO
760  DATA .000069,3.16948,.471239,3.91303,39.44,9.86,5.23114,.300197,1.91812
770  FOR I9 = 1 TO 9: READ P$(I9): NEXT I9
780  DATA  MERCURY,VENUS,EARTH,MARS,JUPITER,SATURN,URANUS,NEPTUNE,PLUTO
790 F = 1
800  REM  CALC DATA FOR PLANETS
810  CLS : PRINT 
820  PRINT "PLANETARY DATA FOR.. ";Y;" ";M;" ";D
830  PRINT "WHICH IS ";NI;" DAYS FROM EPOCH 1960"
840  PRINT "-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:"
850  PRINT "        HELIO     DIST     R.A.    DEC"
860  PRINT "         LONG   TO PLANET  HRS     DEG"
870  PRINT "-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:-:"
880  PRINT :I = 1
890  FOR J = 0 TO 8: GOSUB 1120
900 A(I) = A:D(I) = D:L(I) = L
910 I = I +1: NEXT 
920  FOR I = 1 TO 9
930  REM  SKIP EARTH
940  IF I = 3  THEN I = 4
950  GOSUB 1260
960 Q(I) = Q:R(I) = R:V(I) = V
970  NEXT I
980  FOR I = 1 TO 9:A(I) =  FN DEG(A(I))
990  IF I = 3  THEN  I = 4
1000  PRINT P$(I);
1010  PRINT  TAB( 10); LEFT$( STR$(A(I)),5);
1020  PRINT  TAB( 18); LEFT$( STR$(Q(I)),5);
1030  PRINT  TAB( 27); LEFT$( STR$(R(I)),5);
1040  PRINT  TAB( 34); LEFT$( STR$(V(I)),5)
1050  PRINT 
1060  NEXT 
1070  INPUT "DO YOU WANT ANOTHER DATE? ";A$
1080  IF A$ = "Y" GOTO 280
1090  IF A$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1070
1100  CLS 
1110  RUN "D:ASTRO"
1120  REM  CALC A,D,AND L
1130  REM  CALC HELIOCENTRIC LONG A
1140 A = NI *PD(J,0) +PD(J,1)
1150  IF A >6.28318  THEN A = ((A/6.28318) - INT(A/6.28318)) *6.28318
1160  IF A <0  THEN A = A +6.28318: GOTO 1160
1170 C = PD(J,2) * SIN(A -PD(J,3))
1180 A = A +C
1190  IF A >6.28318  THEN A = A -6.28318
1200  IF A <0  THEN A = A +6.28318: GOTO 1200
1210  REM  CALC DIST OF PLANET FROM SUN D
1220 D = PD(J,4) +PD(J,5) * SIN(A -PD(J,6))
1230  REM  CALC DIST. OF PLANET FROM ECLIPTIC L
1240 L = PD(J,7) * SIN(A -PD(J,8))
1250  RETURN 
1260  REM  CALC RA AND DEC R, V
1270  REM  AND DISTANCE FROM EARTH Q
1280 Z = A(3) -A(I)
1290  IF  ABS(Z) >3.14159  AND Z <0  THEN Z = Z +6.28318
1300  IF  ABS(Z) >3.14159  AND Z >0  THEN Z = Z -6.28318
1310  REM  DISTANCE FROM EARTH
1320 Q =  SQR(D(I) ^2 +D(3) ^2 -2 *D(I) *D(3) * COS(Z))
1330  REM  CALC ANG DISTANCE FROM SUN
1340 P = (D(I) +D(3) +Q)/2
1350 X = 2 * FN ACO( SQR(((P *(P -D(I)))/(D(3) *Q))))
1360  REM  CALC RA
1370  IF Z <0  THEN R =  FN DEG(A(3) +3.14159 -X)/15
1380  IF Z >0  THEN R =  FN DEG(A(3) +3.14159 +X)/15
1390  IF R >24  THEN R = R -24: GOTO 1390
1400  IF R < -24  THEN R = R +24: GOTO 1400
1410  IF R <0  THEN R = R +24: GOTO 1410
1420  REM  CALC DEC.
1430  IF Z <0  THEN V =  SIN(A(3) +3.14159 -X) *23.44194 + FN DEG(L(I))
1440  IF Z >0  THEN V =  SIN(A(3) +3.14159 +X) *23.44194 + FN DEG(L(I))
1450 X =  FN DEG(X)
1460  RETURN 
