10  REM  PSTAR POSITIONS
20  CLS : PRINT : PRINT : PRINT : PRINT 
30  PRINT  TAB( 8)"---------------------"
40  PRINT  TAB( 8)"I      POLARIS      I"
50  PRINT  TAB( 8)"---------------------"
60  PRINT : PRINT 
70  PRINT  TAB( 8)"AN ASTRONOMY PROGRAM"
80  PRINT  TAB( 7)"BY ERIC BURGESS F.R.A.S."
90  PRINT : PRINT 
100  PRINT  TAB( 8)"ALL RIGHTS RESERVED BY"
110  PRINT  TAB( 8)"S & T SOFTWARE SERVICE"
120  PRINT : PRINT 
130  FOR K = 3000 TO 1  STEP  -1: NEXT 
140  CLS : PRINT : PRINT : PRINT 
150  PRINT  TAB( 5)"THIS PROGRAM PROVIDES THE TIMES OF"
160  PRINT 
170  PRINT  TAB( 10)"ELONGATIONS AND TRANSITS"
180  PRINT 
190  PRINT  TAB( 10)"OF POLARIS FOR ANY DATE"
200  PRINT 
210  PRINT  TAB( 10)"WITH SUFFICIENT ACCURACY"
220  PRINT 
230  PRINT  TAB( 13)"(WITHIN 10 MINUTES)"
240  PRINT 
250  PRINT  TAB( 10)"FOR SETTING OR CHECKING": PRINT 
260  PRINT  TAB( 12)"THE ALIGNMENT OF THE": PRINT 
270  PRINT  TAB( 6)"EQUATORIAL MOUNT OF A TELESCOPE"
280  PRINT : PRINT : PRINT 
290  INPUT "PRESS RETURN WHEN READY";A$
300  CLS : PRINT : PRINT : PRINT 
310  INPUT "YEAR ";Y
320  IF Y/4 - INT(Y/4) = 0  AND Y/100 - INT(Y/100) < >0  THEN LY = 1
330  PRINT 
340  INPUT "MONTH ";M
350 D2 = 0
360  IF M = 2  THEN D2 = 31
370  IF M = 3  THEN D2 = 59
380  IF M = 4  THEN D2 = 90
390  IF M = 5  THEN D2 = 120
400  IF M = 6  THEN D2 = 151
410  IF M = 7  THEN D2 = 181
420  IF M = 8  THEN D2 = 212
430  IF M = 9  THEN D2 = 243
440  IF M = 10  THEN D2 = 273
450  IF M = 11  THEN D2 = 304
460  IF M = 12  THEN D2 = 334
470  PRINT : INPUT "DAY ";D1
480 D = D1 +D2
490  IF LY = 1  AND M >2  THEN D = D +1
500  REM  GST AT 0 HR OF EPOCH 1980
510  REM  ADJUST FOR YEAR
520 G = Y -1980
530 YC =  -.01638889 *G
540 GST = 6.65422 +YC
550  REM  ADJUST FOR DAILY GAIN OF SID TIME
560 DG = D *.0657096
570 GT = GST +DG
580  IF GT >24  THEN GT = GT -24
590  IF GT <0  THEN GT = GT +24
600  PRINT : INPUT "LONGITUDE ";LO
610  REM  ADJUST ST FOR LONGITUDE
620 CF = .065556 *LO/360
630  REM  GET HOUR ANGLE
640 GT = GT +GT *CF
650 HA = 2.183333 -GT
660  IF HA <0  THEN HA = HA +24
670  REM  ADJUST FOR DAILY LOSS OF MEAN TIME
680 MT = HA
690 QD = 5.98362:QE = 5.933333
700 QW = 5.933333:QL = 11.967222
710  CLS : PRINT : PRINT 
720  PRINT : PRINT "WHICH ELONGATION OR TRANSIT"
730  PRINT : PRINT 
740  PRINT  TAB( 4)"WEST ELONGATION NEXT (1)"
750  PRINT  TAB( 4)"            PREVIOUS (2)"
760  PRINT  TAB( 4)"EAST ELONGATION NEXT (3)"
770  PRINT  TAB( 4)"            PREVIOUS (4)"
780  PRINT  TAB( 4)"LOWER TRANSIT   NEXT (5)"
790  PRINT  TAB( 4)"UPPER TRANSIT   NEXT (6)"
800  PRINT : PRINT 
810  INPUT "SELECT BY NUMBER ";S$
820 S =  VAL(S$)
830  IF S = 0  OR S >6  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 710
840  CLS : PRINT : PRINT : PRINT : PRINT 
850  PRINT "ON ";Y;" ";M;" ";D1;" LONGITUDE ";LO
860  PRINT 
870  ON S GOTO 880,920,960,1000,1040,1080
880 WN = MT +QW:IN = WN: GOSUB 1280: GOSUB 1190:WN = IN
890 WN =  VAL( LEFT$( STR$(WN),5))
900  PRINT "NEXT WEST ELONGATION WILL BE ";WN;" HRS"
910  GOSUB 1250: GOTO 1110
920 WP = MT -QL -QE:IN = WP: GOSUB 1280: GOSUB 1190:WP = IN
930 WP =  VAL( LEFT$( STR$(WP),5))
940  PRINT "PREVIOUS WEST ELONGATION WAS ";WP;" HRS"
950  GOSUB 1250: GOTO 1110
960 EN = MT +QL +QW:IN = EN: GOSUB 1280: GOSUB 1190:EN = IN
970 EN =  VAL( LEFT$( STR$(EN),5))
980  PRINT "NEXT EAST ELONGATION WILL BE ";EN;" HRS"
990  GOSUB 1250: GOTO 1110
1000 EP = MT -QW:IN = EP: GOSUB 1280: GOSUB 1190:EP = IN
1010 EP =  VAL( LEFT$( STR$(EP),5))
1020  PRINT "PREVIOUS EAST ELONGATION WAS ";EP;" HRS"
1030  GOSUB 1250: GOTO 1110
1040 NLT = MT +QL:IN = NLT: GOSUB 1280: GOSUB 1190:NLT = IN
1050 NLT =  VAL( LEFT$( STR$(NLT),5))
1060  PRINT "NEXT LOWER TRANSIT WILL BE ";NLT;" HRS"
1070  GOSUB 1250: GOTO 1110
1080 NUT = HA:IN = NUT: GOSUB 1280: GOSUB 1190:NUT = IN
1090 NUT =  VAL( LEFT$( STR$(NUT),5))
1100  PRINT "NEXT UPPER TRANSIT WILL BE ";NUT;" HRS": GOSUB 1250
1110  PRINT : PRINT : PRINT 
1120  INPUT "WANT ANOTHER ELONG. OR TRANSIT (Y/N)";A$
1130  IF A$ = "Y"  THEN  CLS : PRINT : PRINT : GOTO 720
1140  PRINT 
1150  INPUT "WANT ANOTHER DATE (Y/N)";A$
1160  IF A$ = "Y"  THEN 300
1170  CLS 
1180  GOTO 1310
1190  REM  CONVERT TO HR,MIN,SEC
1200 IN = IN +IN *.00273043
1210 HM =  INT(IN)
1220 MI = 60 *(IN - INT(IN))
1230 M2 =  INT(MI)
1240  RETURN 
1250  PRINT 
1260  PRINT  TAB( 20)"OR ";HM;" HR ";M2;" MI"
1270  RETURN 
1280  IF IN >24  THEN IN = IN -24
1290  IF IN <0  THEN IN = IN +24
1300  RETURN 
1310  RUN "D:ASTRO"
