10  DEF  FN ACO(X) =  - ATN(X/ SQR( -X *X +1)) +1.5707963
20 LY = 0
30 FL = 0
40 P1 = 3.14159:P2 = 6.28318
50  CLS : PRINT : PRINT : PRINT 
60  PRINT  TAB( 10)"----------------"
70  PRINT  TAB( 10)"I    M A R S   I"
80  PRINT  TAB( 10)"----------------"
90  PRINT : PRINT 
100  PRINT  TAB( 8)"AN ASTRONOMY PROGRAM": PRINT : PRINT 
110  PRINT  TAB( 7)"BY ERIC BURGESS F.R.A.S.": PRINT : PRINT 
120  PRINT  TAB( 8)"ALL RIGHTS RESERVED BY"
130  PRINT  TAB( 8)"S & T SOFTWARE SERVICE": PRINT : PRINT 
140  PRINT 
150  INPUT "DO YOU WANT INSTRUCTION (Y/N)";A$
160  IF A$ = "N"  THEN  CLS : PRINT : PRINT : PRINT : GOTO 250
170  IF A$ < >"Y"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 150
180  CLS : PRINT : PRINT : PRINT 
190  PRINT "THIS PROGRAM OFFERS TWO ALTERNATIVES": PRINT 
200  PRINT "  1) CALCULATES APPROXIMATE ANGULAR"
210  PRINT "     DIAMETER OF MARS FOR ANY DATE"
220  PRINT "  2) PROVIDES APPROXIMATE DATE OF"
230  PRINT "     NEXT OPPOSITION AFTER ANY DATE": PRINT 
240  PRINT : PRINT : PRINT 
250  PRINT "SELECT 1) ANGULAR DIAMETER"
260  PRINT "       2) NEXT OPPOSITION"
270  PRINT 
280  INPUT B$
290  IF B$ = "1"  THEN 320
300  IF B$ < >"2"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 250
310  GOTO 1390
320  GOSUB 670: REM  GET DISTANCE FROM EARTH
330  CLS : PRINT : PRINT 
340 MD$ =  LEFT$( STR$(9.31/Q),5)
350  PRINT "FOR YEAR ";Y;" MONTH ";M;" DAY ";D: PRINT 
360  PRINT "--------------------------": PRINT 
370 Q$ =  LEFT$( STR$(Q),4)
380  PRINT "DISTANCE OF MARS IS ";Q$;" A.U."
390  PRINT "        OR "; LEFT$( STR$(Q *92.96),4);" MILLION MILES"
400  PRINT 
410 MW$ =  LEFT$( STR$(W *57.29578),4)
420  PRINT "ANG. DISTANCE FROM SUN IS ";MW$;
430  PRINT " DEGREES": PRINT 
440  IF  VAL(MD$) >25  THEN MD$ = "25"
450  PRINT "ANG DIAM OF MARS IS ";MD$;
460  PRINT " SEC OF ARC"
470  PRINT "  MAX AT CLOSEST OPPOSITION IS 25 SEC"
480  PRINT 
490  PRINT "COMPARISONS ARE......"
500  PRINT 
510  PRINT "MAXIMUM DIAM (-----------------------)
520  PRINT " AT OPPOSITION"
530  IF 9.36/Q > = 25  THEN OP = 24: GOTO 550
540 OP =  INT(9.36/Q)
550  PRINT "DIAM AT DATE (";
560  PRINT  TAB( 14 +OP)")"
570  PRINT "MINIMUM DIAM (-)
580  PRINT " AT CONJUNCTION"
590  PRINT : PRINT 
600  INPUT "WANT ANOTHER DATE ";A$
610  IF A$ = "N"  THEN  CLS : GOTO 660
620  IF A$ < >"Y"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 600
630  CLS :FL = 0: PRINT : PRINT : PRINT 
640  IF B$ = "1"  THEN  GOTO 320
650  GOTO 1390
660  RUN "D:ASTRO"
670  REM  ROUTINE FOR ANGULAR DIAMETER
680  GOSUB 930: REM  ENTER DATE
690  GOSUB 1180
700  REM  CALC DISTANCE FROM EARTH
710  REM  AND ANGULAR DISTANCE FROM SUN
720 AE = NI *.017202 +1.74022
730  IF AE >P2  THEN AE = ((AE/P2) - INT(AE/P2)) *P2
740  IF AE <0  THEN AE = AE +P2: GOTO 740
750 CE = .032044 * SIN(AE -1.78547)
760 AE = AE +CE
770  IF AE >P2  THEN AE = AE -P2
780  IF AE <0  THEN AE = AE +P2: GOTO 780
790 DE = 1 +.017 * SIN(AE -3.33926)
800  GOSUB 1290
810 Z = AE -MA
820  IF  ABS(Z) >P1  AND Z <0  THEN Z = Z +P2
830  IF  ABS(Z) >P1  AND Z >0  THEN Z = Z -P2
840  IF Z = 0  THEN Q = MD -DE:W = P1: GOTO 920
850  IF Z = P1  THEN Q = MD +DE:W = 0: GOTO 920
860  REM  CALC DISTANCE (Q) FROM EARTH (AU)
870 Q =  SQR(MD ^2 +DE ^2 -2 *MD *DE * COS(Z))
880 P = (MD +DE +Q)/2
890  REM  CALC ANGULAR DISTANCE (W) FROM SUN
900 V =  SQR((P *(P -MD))/(DE *Q))
910 W = 2 * FN ACO(V)
920  RETURN 
930  REM  ENTER DATE
940  IF B$ = "2"  AND FL = 1  THEN  GOTO 1160
950  PRINT : PRINT 
960  PRINT "ENTER THE DATE": PRINT 
970  INPUT " THE YEAR ";Y$:Y =  VAL(Y$)
980  IF Y = 0  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 970
990  IF Y >1800 GOTO 1040
1000  PRINT "IS ";Y;" THE CORRECT YEAR": INPUT A$
1010  IF A$ = "Y"  THEN 1040
1020  IF A$ < >"N"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1000
1030  IF A$ = "N"  THEN 970
1040  REM  CHECK FOR LEAP YEAR
1050  GOSUB 1070
1060  GOTO 1100
1070 Y4 = Y/4:Y5 = Y/100
1080  IF Y4 - INT(Y4) = 0  AND Y5 - INT(Y5) < >0  THEN LY = 1
1090  RETURN 
1100  PRINT : INPUT " THE MONTH ";M$:M =  VAL(M$)
1110  IF M = 0  OR M >12  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1070
1120  PRINT : INPUT " THE DAY ";D$:D =  VAL(D$)
1130  IF D = 0  OR D >31  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1120
1140  IF M = 2  AND D >29  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1120
1150  REM  STORE INITIAL DATE
1160 YI = Y:MI = M:DI = D
1170  RETURN 
1180  REM  CALC DAYS FROM EPOCH
1190 DG = 365 *Y +D +((M -1) *31)
1200  IF M > = 3 GOTO 1240
1210  REM  CALC IF JAN OR FEB
1220 DG = DG + INT((Y -1)/4) - INT((.75) * INT((Y -1)/100 +1))
1230  GOTO 1260
1240  REM  CALC FOR MAR THRU DEC
1250 DG = DG - INT(M *.4 +2.3) + INT(Y/4) - INT((.75) * INT((Y/100) +1))
1260 NI = DG -715875
1270  RETURN 
1280  GOTO 320
1290  REM  CALC MARS'S POSITION
1300 MA = NI *.009146 +4.51234
1310  IF MA >P2  THEN MA = ((MA/P2) - INT(MA/P2)) *P2
1320  IF MA <0  THEN MA = MA +P2: GOTO 1320
1330 MC = .175301 * SIN(MA -5.85209)
1340 MA = MA +MC
1350  IF MA >P2  THEN MA = MA -P2
1360  IF MA <0  THEN MA = MA +P2: GOTO 1360
1370 MD = 1.5237 +.141704 * SIN(MA -1.04656)
1380  RETURN 
1390  GOSUB 930: GOSUB 1180
1400  GOSUB 710
1410  PRINT : PRINT 
1420  CLS : PRINT : PRINT : PRINT 
1430  PRINT AT (0,22)
1440  PRINT YI;" ";MI;" ";DI
1450  GOSUB 1980: REM  TO PLOT PLANETS
1460 FL = 1
1470 DS = W *57.29578
1480  IF DS >120  THEN  GOTO 1540
1490 FL = 1
1500 M = M +1
1510  IF M >12  THEN M = 1:Y = Y +1
1520  GOSUB 1070
1530  GOTO 1390
1540  REM  COMPUTE FOR DAYS
1550  IF DS <160  THEN D = D +5: GOTO 1580
1560  IF DS <170  THEN D = D +2: GOTO 1580
1570 D = D +1
1580  GOSUB 1630
1590 D2 = DS
1600  IF DS >178  THEN  GOTO 1710
1610  IF D2 <DS  THEN D = D +1: GOSUB 1630
1620  GOTO 1700
1630  REM  CORRECT FOR MONTH AND YEAR ENDS
1640  IF (LY = 1  AND M = 2  AND D >29)  THEN M = 3:D = D -29: GOTO 1640
1650  IF (LY = 0  AND M = 2  AND D >28)  THEN M = 3:D = D -28: GOTO 1650
1660  IF D <31  THEN  GOTO 1690
1670  IF (M = 4  OR M = 6  OR M = 9  OR M = 11)  THEN M = M +1:D = D -30: GOTO 1690
1680  IF D >31  THEN M = M +1:D = D -31
1690  RETURN 
1700  GOTO 1390
1710  REM  COMPUTE RA FOR YI,MI,DI
1720  IF Z <0  THEN R = AE +P1 -W
1730  IF Z >0  THEN R = AE +P1 +W
1740 R = 57.29578 *R/15
1750  IF R >24  THEN R = R -24: GOTO 1750
1760  IF R < -24  THEN R = R +24: GOTO 1760
1770  IF R <0  THEN R = R +24: GOTO 1770
1780  IF R >0  AND R <2  THEN C$ = "PISCES": GOTO 1900
1790  IF R >2  AND R <4  THEN C$ = "ARIES": GOTO 1900
1800  IF R >4  AND R <6  THEN C$ = "TAURUS": GOTO 1900
1810  IF R >6  AND R <8  THEN C$ = "GEMINI": GOTO 1900
1820  IF R >8  AND R <10  THEN C$ = "CANCER": GOTO 1900
1830  IF R >10  AND R <12  THEN C$ = "LEO": GOTO 1900
1840  IF R >12  AND R <14  THEN C$ = "VIRGO": GOTO 1900
1850  IF R >14  AND R <16  THEN C$ = "LIBRA": GOTO 1900
1860  IF R >16  AND R <18  THEN C$ = "SCORPIO": GOTO 1900
1870  IF R >18  AND R <20  THEN C$ = "SAGITTARIUS": GOTO 1900
1880  IF R >20  AND R <22  THEN C$ = "CAPRICORNUS": GOTO 1900
1890  IF R >22  AND R <24  THEN C$ = "AQUARIUS"
1900  PRINT 
1910  GRAPHICS 0
1920  PRINT "NEXT OPPOSITION OF MARS IS"
1930  PRINT "        "YI;" ";MI;" ";DI
1940  PRINT "IN THE CONSTELLATION OF ";C$
1950  PRINT 
1960  INPUT "FOR FURTHER DETAILS PRESS RETURN KEY";Q$
1970 Y = YI:M = MI:D = DI: GOTO 320
1980  REM  SUB TO PLOT PLANETS
1990 AD = 50:DM = 75
2000 EY = AD * SIN(AE)
2010 EX = 1.5 *AD * COS(AE)
2020 MY = DM * SIN(MA)
2030 MX = 1.5 *DM * COS(MA)
2040  GRAPHICS 8 : SETCOLOR 6,0,0 : SETCOLOR 5,0,15 : COLOR 1
2050  PLOT 138,78: PLOT 139,79: PLOT 137,77
2060  PLOT 136,76: PLOT 140,80: PLOT 139,81
2070  PLOT 138,81: PLOT 137,81: PLOT 136,80
2080  PLOT 138,75: PLOT 137,75: PLOT 139,75
2090  PLOT 140,76: PLOT 139,75: PLOT 140,76
2100  FOR J = 150 TO 250  STEP 10
2110  PLOT J,78 TO J +5,78
2120  NEXT J
2130  PLOT (138 +EX),(78 -EY)
2140  PLOT (138 +MX),(78 -MY)
2150  PLOT 269,76: PLOT 270,79: PLOT 270,78
2160  PLOT 270,77: PLOT 269,76: PLOT 268,76
2170  PLOT 267,77: PLOT 271,76: PLOT 272,76
2180  PLOT 273,77
2190  RETURN 
