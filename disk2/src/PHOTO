10  REM  PHOTO GUIDE FOR PLANETS
20  CLS : PRINT : PRINT : PRINT : PRINT 
30  PRINT : PRINT 
40  PRINT  TAB( 9)"-----------------"
50  PRINT  TAB( 9)"I     PHOTO     I"
60  PRINT  TAB( 9)"-----------------"
70  PRINT : PRINT 
80  PRINT  TAB( 7)"AN ASTRONOMY PROGRAM"
90  PRINT : PRINT  TAB( 6)"BY ERIC BURGESS F.R.A.S."
100  PRINT : PRINT 
110  PRINT  TAB( 6)"ALL RIGHTS RESERVED BY"
120  PRINT  TAB( 6)"S & T SOFTWARE SERVICE"
130  PRINT : PRINT 
140  PRINT : PRINT 
150  INPUT "WANT INSTRUCTIONS? Y/N ";A$
160  IF A$ = "N"  THEN  GOTO 310
170  IF A$ < >"Y"  THEN  PRINT "INVALID RESPONSE"; PRINT : GOTO 150
180  CLS : PRINT 
190  PRINT : PRINT : PRINT 
200  PRINT "THIS PROGRAM ASKS FOR PARTICULARS OF"
210  PRINT "YOUR ASTROPHOTO INSTALLATION AND THEN"
220  PRINT "COMPUTES THE SIZE OF THE IMAGE OF ANY"
230  PRINT "SELECTED PLANET ON THE FILM PLANE"
240  PRINT : PRINT : PRINT 
250  PRINT "FOR AN ASA RATING WHICH YOU INPUT"
260  PRINT "IT CALCULATES AN AVERAGE EXPOSURE"
270  PRINT "FOR THE PLANET AND THE INSTRUMENT"
280  PRINT "CONFIGURATION YOU ARE USING."
290  PRINT : PRINT : PRINT 
300  INPUT "PRESS RETURN TO CONTINUE ";A$
310  CLS : PRINT : PRINT 
320  PRINT "WHICH METHOD ARE YOU USING?"
330  PRINT : PRINT 
340  PRINT " 1) FILM AT PRIME FOCUS OF"
350  PRINT "    TELESCOPE'S OBJECTIVE"
360  PRINT 
370  PRINT " 2) FILM IN CAMERA AND CAMERA"
380  PRINT "    AT EYEPIECE OF TELESCOPE"
390  PRINT "    WITH CAMERA LENS IN USE"
400  PRINT 
410  PRINT " 3) IMAGE PROJECTED FROM EYEPIECE"
420  PRINT "    OF TELESCOPE INTO CAMERA"
430  PRINT "    WITHOUT A CAMERA LENS"
440  PRINT : PRINT 
450  INPUT "SELECT 1, 2, OR 3 ";A
460  CLS : PRINT : PRINT 
470  PRINT "GIVE FOLLOWING DATA "
480  PRINT 
490  INPUT "CLEAR APERTURE OF OBJECTIVE (IN) ";AO
500 AO = AO *25.4
510  PRINT 
520  INPUT "FOCAL LENGTH OF OBJECTIVE (IN) ";FO
530 FO = FO *25.4
540 NF = FO/AO
550  IF A < >2  THEN  GOTO 580
560  PRINT 
570  INPUT "FOCAL LENGTH OF CAMERA LENS (MM) ";FC
580  IF A = 1  THEN  GOTO 670
590  PRINT 
600  INPUT "FOCAL LENGTH OF EYEPIECE (MM) ";FE
610  IF A = 1  THEN  GOTO 670
620  IF A = 2  THEN  GOTO 670
630  PRINT 
640  INPUT "DISTANCE EYEPIECE TO FILM PLANE (IN) ";DE
650 DE = DE *25.4
660  REM  CALC. EFFECTIVE F NUMBER
670  IF A = 1  THEN EFL = NF
680  IF A = 2  THEN EFL = NF *FC/FE: GOTO 700
690  IF A = 3  THEN EFL = (DE -1)/FE *NF
700  CLS : PRINT : PRINT 
710  PRINT "YOU MUST NOW ENTER THE NAME OF"
720  PRINT "THE PLANET YOU ARE PHOTOGRAPHING"
730  PRINT "AND ITS DIAMETER IN ARCSECS"
740  PRINT 
750  PRINT "(YOU CAN FIND THIS FROM ONE OF THE"
760  PRINT " PROGRAMS IN THIS SERIES)"
770  PRINT : PRINT 
780  INPUT "WHAT IS PLANET'S NAME ";PL$
790  IF PL$ = "PLUTO"  THEN  PRINT "NO DATA ON PLUTO; PICK ANOTHER PLANET ": GOTO 780
800  PRINT 
810  INPUT "WHAT IS DIAMETER IN ARCSECS ";DP
820 F = EFL *AO
830  CLS : PRINT : PRINT : PRINT 
840 DP = DP/3600
850 DP =  TAN(DP/57.29578)
860 I = F *DP
870 R = 1450/EFL
880 R =  VAL( LEFT$( STR$(R),4))
890  PRINT : PRINT 
900  PRINT "RESOLUTION FOR ";PL$;" AT"
910  PRINT "FILM PLANE WILL BE "; INT(R);" LINES/MM"
920  PRINT : PRINT 
930  PRINT "YOU ARE ADVISED TO PICK A FILM WITH"
940  PRINT "AT LEAST THREE TIMES THIS RESOLUTION"
950 R =  VAL( LEFT$( STR$(R *3),4))
960  PRINT : PRINT "NAMELY "; INT(R);" LINES/MM TO AVOID"
970  PRINT "GRAIN SPOILING THE IMAGE"
980  PRINT : PRINT : PRINT 
990  INPUT "WHAT IS ASA RATING OF CHOSEN FILM? ";ASA
1000  CLS : PRINT : PRINT 
1010  IF PL$ = "MERCURY"  THEN N = 260
1020  IF PL$ = "VENUS"  THEN N = 960
1030  IF PL$ = "MARS"  THEN N = 40
1040  IF PL$ = "JUPITER"  THEN N = 12
1050  IF PL$ = "SATURN"  THEN N = 3.5
1060  IF PL$ = "URANUS"  THEN N = .81
1070  IF PL$ = "NEPTUNE"  THEN N = .38
1080 N = N *ASA
1090 EX = EFL *EFL/N
1100  CLS : PRINT : PRINT 
1110  PRINT "TO PHOTOGRAPH ";PL$;" ON FILM RATED AT "
1120  PRINT ASA;" ASA"
1130  PRINT 
1140  IF A = 1  THEN EQ$ = "FILM AT PRIME FOCUS OF OBJECTIVE"
1150  IF A = 2  THEN EQ$ = "CAMERA/EYEPIECE COMBINATION"
1160  IF A = 3  THEN EQ$ = "PROJECTION FROM EYEPIECE"
1170  PRINT "WITH ";EQ$
1180  PRINT : PRINT 
1185  IF EX <.01  THEN  PRINT "THE EXPOSURE IS ";EX;" SEC.": GOTO 1210
1190 EX =  VAL( LEFT$( STR$(EX),3))
1200  PRINT "THE EXPOSURE IS ABOUT ";EX;" SEC"
1210  PRINT 
1220  PRINT "    (NOTE: EXPOSURE GREATER THAN 1 SEC"
1230  PRINT "     IS NOT RECOMMENDED)"
1240  PRINT 
1250  PRINT "AND THE IMAGE ON THE FILM PLANE WILL BE"
1260 I =  VAL( LEFT$( STR$(I),3))
1270  PRINT I;" MM IN DIAMETER"
1280  PRINT 
1290  PRINT "    (NOTE: IMAGE ON THE FILM PLANE"
1300  PRINT "     SHOULD EXCEED 3 MM TO BE USEFUL)"
1310  PRINT : PRINT 
1320  INPUT "WANT ANOTHER CALCULATION? Y/N ";A$
1330  IF A$ = "N"  THEN  GOTO 1370
1340  IF A$ < >"Y"  THEN  PRINT "INVALID RESPONSE": PRINT : GOTO 1320
1350  GOTO 310
1360  CLS 
1370  RUN "D:ASTRO2"
